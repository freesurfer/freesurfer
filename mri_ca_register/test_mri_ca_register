#!/bin/tcsh -f

#
# test_mri_ca_register
#
# execute mri_ca_register and check results against expected results
#
# Original Author: Nick Schmansky
# CVS Revision Info:
#    $Author: zkaufman $
#    $Date: 2016/03/17 06:39:55 $
#    $Revision: 1.5 $
#
# Copyright Â© 2015 The General Hospital Corporation (Boston, MA) "MGH"
#
# Terms and conditions for use, reproduction, distribution and contribution
# are found in the 'FreeSurfer Software License Agreement' contained
# in the file 'LICENSE' found in the FreeSurfer distribution, and here:
#
# https://surfer.nmr.mgh.harvard.edu/fswiki/FreeSurferSoftwareLicense
#
# Reporting: freesurfer@nmr.mgh.harvard.edu
#
# General inquiries: freesurfer@nmr.mgh.harvard.edu
#

set VERSION='$Id: test_mri_ca_register,v 1.5 2016/03/17 06:39:55 zkaufman Exp $'

umask 002

if ($SKIP_MRI_CA_REGISTER_TEST) then
  exit 0
endif


#
# extract testing data
#
gunzip -c testdata.tar.gz | tar xvf -

#
# run mri_ca_register using typical input file fommats
#

cd testdata

setenv FREESURFER_HOME ../../distribution
setenv SUBJECTS_DIR ""

setenv OMP_NUM_THREADS 4

# usage in recon-all: 
set cmd=(../mri_ca_register \
    -nobigventricles \
    -T talairach.lta \
    -align-after \
    -mask brainmask.mgz \
    norm.mgz \
    $FREESURFER_HOME/average/RB_all_2016-02-11.gca \
    talairach.m3z)

echo ""
echo $cmd
$cmd
if ($status != 0) then
  echo "mri_ca_register FAILED"
  exit 1
endif

#
# compare expected results with actual (produced) results
#

set OS=`uname -s`
if ("$OS" == "Darwin") then
  set REF_VOL = ref_talairach_osx.m3z
else
  set REF_VOL = ref_talairach.m3z
endif
set TST_VOL = talairach.m3z
set cmd=(../../mri_diff/mri_diff --debug \
            $REF_VOL $TST_VOL );
echo ""
echo $cmd
$cmd
set diff_status=$status
if ($diff_status != 0) then
  echo "$cmd FAILED (exit status=$diff_status)"
  exit 1
endif

if (-e ../mri_ca_register_cuda && -e TEST_CUDA) then
  
  # Source cuda setup file which set the path
  source ../../scripts/cuda_setup

  set cmd=(../mri_ca_register_cuda \
    -nobigventricles \
    -T talairach.lta \
    -align-after \
    -mask brainmask.mgz \
    norm.mgz \
    $FREESURFER_HOME/average/RB_all_2016-02-11.gca \
    talairach_cuda.m3z)

  echo ""
  echo $cmd
  $cmd
  if ($status != 0) then
    echo "mri_ca_register_cuda FAILED"
    exit 1
  endif

  if (-e talairach_cuda.m3z) then 
    set OS=`uname -s`
    if ("$OS" == "Darwin") then
      set REF_VOL = ref_talairach_osx_cuda.m3z
    else
      set REF_VOL = ref_talairach_cuda.m3z
    endif
    set TST_VOL = talairach_cuda.m3z
    set cmd=(../../mri_diff/mri_diff --debug \
            $REF_VOL $TST_VOL );
    echo ""
    echo $cmd
    #$cmd
    set diff_status=$status
    if ($diff_status != 0) then
      echo "$cmd FAILED (exit status=$diff_status)"
      exit 1
    endif
  endif

endif

echo ""
echo ""
echo ""

#
# cleanup
#
cd ..
rm -Rf testdata

echo ""
echo "test_mri_ca_register passed all tests"
exit 0
