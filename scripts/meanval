#!/bin/tcsh -f
# meanval

set VERSION = 'meanval @FS_VERSION@';

set meanval = ();
set avgwf = ();
set maskvol = ();
set invol = ();
set tmpdir = ();
set cleanup = 1;
set LF = ();

set inputargs = ($argv);
set PrintHelp = 0;

if($#argv == 0) goto usage_exit;
set n = `echo $argv | grep -e -help | wc -l` 
if($n != 0) then
  set PrintHelp = 1;
  goto usage_exit;
endif
set n = `echo $argv | grep -e -version | wc -l` 
if($n != 0) then
  echo $VERSION
  exit 0;
endif

source $FREESURFER_HOME/sources.csh

goto parse_args;
parse_args_return:

goto check_params;
check_params_return:

set outdir = `dirname $meanval`;
mkdir -p $outdir
pushd $outdir > /dev/null
set outdir = `pwd`;
popd > /dev/null

if($#tmpdir == 0) set tmpdir = $outdir/tmpdir.meanval.$$
mkdir -p $tmpdir

if($#LF == 0) set LF = $tmpdir/meanval.log
if($LF != /dev/null) rm -f $LF

echo "Log file for meanval" >> $LF
date  | tee -a $LF
echo "" | tee -a $LF
echo "setenv SUBJECTS_DIR $SUBJECTS_DIR" | tee -a $LF
echo "cd `pwd`"  | tee -a $LF
echo $0 $inputargs | tee -a $LF
echo "" | tee -a $LF
cat $FREESURFER_HOME/build-stamp.txt | tee -a $LF
uname -a  | tee -a $LF

set cmd = (mri_segstats --seg $maskvol --id 1 --sum $tmpdir/blah.$$ \
            --in $invol --sfavg $meanval )
if($#avgwf) set cmd = ($cmd --avgwf $avgwf)
echo $cmd
$cmd
if($status) exit 1;

if($cleanup) rm -rf $tmpdir

exit 0

###############################################

############--------------##################
parse_args:
set cmdline = ($argv);
while( $#argv != 0 )

  set flag = $argv[1]; shift;
  
  switch($flag)

    case "--i":
      if($#argv < 1) goto arg1err;
      set invol = $argv[1]; shift;
      if(! -e $invol) then
        echo "ERROR: cannot find $invol"
        exit 1;
      endif
      breaksw

    case "--m":
      if($#argv < 1) goto arg1err;
      set maskvol = $argv[1]; shift;
      if(! -e $maskvol) then
        echo "ERROR: cannot find $maskvol"
        exit 1;
      endif
      breaksw

    case "--o":
      if($#argv < 1) goto arg1err;
      set meanval = $argv[1]; shift;
      breaksw

    case "--avgwf":
      if($#argv < 1) goto arg1err;
      set avgwf = $argv[1]; shift;
      breaksw

    case "--log":
      if($#argv < 1) goto arg1err;
      set LF = $argv[1]; shift;
      breaksw

    case "--nolog":
    case "--no-log":
      set LF = /dev/null
      breaksw

    case "--tmpdir":
      if($#argv < 1) goto arg1err;
      set tmpdir = $argv[1]; shift;
      set cleanup = 0;
      breaksw

    case "--nocleanup":
      set cleanup = 0;
      breaksw

    case "--cleanup":
      set cleanup = 1;
      breaksw

    case "--debug":
      set verbose = 1;
      set echo = 1;
      breaksw

    default:
      echo ERROR: Flag $flag unrecognized. 
      echo $cmdline
      exit 1
      breaksw
  endsw

end

goto parse_args_return;
############--------------##################

############--------------##################
check_params:

if($#meanval == 0) then
  echo "ERROR: must spec output --o"
  exit 1;
endif
if($#invol == 0) then
  echo "ERROR: must spec input --i"
  exit 1;
endif
if($#maskvol == 0) then
  echo "ERROR: must spec mask --m"
  exit 1;
endif

goto check_params_return;
############--------------##################

############--------------##################
arg1err:
  echo "ERROR: flag $flag requires one argument"
  exit 1
############--------------##################

############--------------##################
usage_exit:
  echo ""
  echo "meanval --i invol --m mask --o meanval.dat"
  echo "  --avgwf avgwf"
  echo ""

  if(! $PrintHelp) exit 1;

  echo $VERSION

  cat $0 | awk 'BEGIN{prt=0}{if(prt) print $0; if($1 == "BEGINHELP") prt = 1 }'

exit 1;


#---- Everything below here is printed out as part of help -----#
BEGINHELP
