#!/bin/csh -f
# xhemireg

# Create a new subject from an existing subject by left-right reversal
# of the volumes.
#
# surfaces: 
#  Apply xfm: orig, white, pial, smoothwm, inflated
#  Copy to contra hemi: area, area.pial, curv, curv.pial, avg_curv, 
#     inflated.{H,K}, sulc, thickness, jacobian_white, aparc, aparc.a2005s
#  Regenerate: qsphere, sphere, sphere.reg
#  Not sure: before, after, defect, uncorrected, *.nofix
#
# volumes:
#  Dont convert volumes in mri/orig
#

set VERSION = '$Id: xhemireg,v 1.4 2007/12/14 22:23:46 greve Exp $';

set subject = ();
set srchemi = ();
set trghemi = ();
set surfreg = xhemi.reg
set UseNoNeg = 0
set NoRandomness = 1;
set RngSeed = 1234
set PrintHelp = 0;
set outdir = ();

# For cortical registration, as found in $AvgCurvTifPath/$hemi.$AvgCurvTif
set AvgCurvTifPath = "$FREESURFER_HOME/average"
set AvgCurvTif = average.curvature.filled.buckner40.tif

set cmdargs = ($argv);
if($#argv == 0) goto usage_exit;
set n = `echo $argv | egrep -e -version | wc -l`
if($n != 0) then
  echo $VERSION
  exit 0;
endif
set n = `echo $argv | egrep -e -help | wc -l`
if($n != 0) then
  set PrintHelp = 1;
  goto usage_exit;
endif

goto parse_args;
parse_args_return:
goto check_params;
check_params_return:

cd $SUBJECTS_DIR/$subject

set xsubject = $subject/xhemi
if($#outdir == 0) set outdir = $SUBJECTS_DIR/$xsubject
echo "outdir is $outdir" 
mkdir -p $outdir/mri $outdir/surf $outdir/label $outdir/scripts

set LF = $SUBJECTS_DIR/$xsubject/xhemireg.$srchemi.log
if(-e $LF) mv $LF $LF.bak
echo "Logfile is $LF"

echo $0 | tee -a $LF
echo $cmdargs | tee -a $LF
echo $VERSION | tee -a $LF
date | tee -a $LF
uname -a | tee -a $LF
pwd | tee -a $LF
echo "setenv SUBJECTS_DIR $SUBJECTS_DIR "| tee -a $LF


#--------------------------------------------------------------
echo "" | tee -a $LF
echo "" | tee -a $LF
date | tee -a $LF
foreach vol (orig aseg)
  set cmd = (mri_convert mri/$vol.mgz $outdir/mri/$vol.mgz --left-right-reverse)
  echo $cmd  | tee -a $LF
  $cmd | tee -a $LF
  if($status) exit 1;
end


#--------------------------------------------------------------
echo ""| tee -a $LF
echo ""| tee -a $LF
date| tee -a $LF
set reg = $outdir/lrrev.register.dat
set cmd = (tkregister2_cmdl --targ mri/orig.mgz \
         --mov $outdir/mri/orig.mgz \
         --reg $reg --regheader --noedit)
echo $cmd | tee -a $LF
$cmd | tee -a $LF
if($status) exit 1;


#--------------------------------------------------------------
foreach surf (orig white pial smoothwm inflated sphere)
  echo "" | tee -a $LF
  echo "" | tee -a $LF
  date | tee -a $LF
  set cmd = (mri_surf2surf --s $subject --sval-xyz $surf \
   --hemi $srchemi --tval-xyz --tval $outdir/surf/$trghemi.$surf \
   --reg $reg)
  echo $cmd  | tee -a $LF
  $cmd | tee -a $LF
  if($status) exit 1;
end

#--------------------------------------------------------------
foreach ov (curv sulc area area.pial curv.pial avg_curv \
     inflated.H inflated.K thickness )
  if(! -e surf/$srchemi.$ov) continue;
  echo "" | tee -a $LF
  echo "" | tee -a $LF
  date | tee -a $LF
  set cmd = (cp surf/$srchemi.$ov $outdir/surf/$trghemi.$ov)
  echo $cmd  | tee -a $LF
  $cmd | tee -a $LF
  if($status) exit 1;
end

#------------------------------------------------------------------
set cmd = (recon-all -sb $xsubject -hemi $trghemi -surfreg -cortparc)
echo "" | tee -a $LF
echo "" | tee -a $LF
date | tee -a $LF
echo $cmd  | tee -a $LF
$cmd | tee -a $LF
if($status) exit 1;

#------------------------------------------------------------------
if(0) then
echo "" | tee -a $LF
echo "" | tee -a $LF
date | tee -a $LF
set AvgTif = ${AvgCurvTifPath}/$trghemi.${AvgCurvTif}
set cmd = (mris_register -curv)
if($UseNoNeg) set cmd = ($cmd -remove_negative 1)
set cmd = ($cmd $outdir/surf/$trghemi.sphere $AvgTif $outdir/surf/$trghemi.sphere.reg)
echo $cmd  | tee -a $LF
$cmd | tee -a $LF
if($status) exit 1;
echo "" | tee -a $LF
echo "" | tee -a $LF
date | tee -a $LF
set GCS = curvature.buckner40.filled.desikan_killiany.2007-06-20.gcs 
set CPAtlas = $FREESURFER_HOME/average/$trghemi.$GCS
set annot = $xsubject/label/$trghemi.aparc.annot
set cmd = (mris_ca_label -seed 1234 -aseg $xsubject/mri/aseg.mgz \
             $xsubject $trghemi  $xsubject/surf/$trghemi.sphere.reg \
             $CPAtlas $annot)
echo $cmd  | tee -a $LF
$cmd | tee -a $LF
if($status) exit 1;
endif

#--------------------------------------------------------------
cp $outdir/surf/$trghemi.sphere.reg surf/$srchemi.$trghemi.xhemi.reg |& tee -a $LF
if($status) exit 1;


#--------------------------------------------------------------
echo "" | tee -a $LF
echo "" | tee -a $LF
date | tee -a $LF
echo "xhemireg done"  | tee -a $LF

exit 0

############--------------##################
parse_args:
set cmdline = ($argv);
while( $#argv != 0 )

  set flag = $argv[1]; shift;

  switch($flag)

    case "--s":
      if ( $#argv < 1) goto arg1err;
      set subject = $argv[1]; shift;
      breaksw

    case "--hemi":
      if ( $#argv < 1) goto arg1err;
      set srchemi = $argv[1]; shift;
      breaksw

    case "--lh":
      set srchemi = lh;
      set trghemi = rh;
      breaksw

    case "--rh":
      set srchemi = rh;
      set trghemi = lh;
      breaksw

    case "--reg":
      if ( $#argv < 1) goto arg1err;
      set surfreg = $argv[1]; shift;
      breaksw

    case "--o":
      if ( $#argv < 1) goto arg1err;
      set outdir = $argv[1]; shift;
      breaksw

    case "--debug":
      set verbose = 1;
      set echo = 1;
      breaksw

    default:
      echo ERROR: Flag $flag unrecognized.
      echo $cmdline
      exit 1
      breaksw
  endsw

end

goto parse_args_return;
############--------------##################

############--------------##################
check_params:

if($#subject == 0) then
  echo "ERROR: must spec a subject id"
  exit 1;
endif

if(! -e $SUBJECTS_DIR/$subject) then
  echo "ERROR: cannot find $subject"
  exit 1;
endif

if($#srchemi == 0) then
  echo "ERROR: must spec a hemi"
  exit 1;
endif


goto check_params_return;
############--------------##################

############--------------##################
arg1err:
  echo "ERROR: flag $flag requires one argument"
  exit 1
############--------------##################

############--------------##################
arg2err:
  echo "ERROR: flag $flag requires two arguments"
  exit 1
############--------------##################

############--------------##################
usage_exit:
  echo ""
  echo "xhemireg"
  echo ""
  echo "   --s subject"
  echo "   --lh : srchemi = lh (map from left to right) "
  echo "   --rh : srchemi = rh (map from right to left) "
  echo "   --reg surfreg : output will be srchemi.trghemi.surfreg (def xhemi.reg)"
  echo ""
  echo "   --o outdir  : output dir (subject/xhemireg.hemi by def)"
  echo "   --version : print version and exit"
  echo "   --help    : print help and exit"
  echo ""

  if($PrintHelp) \
  cat $0 | awk 'BEGIN{prt=0}{if(prt) print $0; if($1 == "BEGINHELP") prt = 1 }'

exit 1;

#---- Everything below here is printed out as part of help -----#
BEGINHELP

