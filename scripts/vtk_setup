#! /bin/tcsh -ef

#
# vtk_setup
#
# Setup VTK for apps using the VTK package included with Freesurfer
#
# Original Author: Nick Schmansky
# CVS Revision Info:
#    $Author: nicks $
#    $Date: 2012/10/15 19:41:34 $
#    $Revision: 1.15 $
#
# Copyright Â© 2011-2012 The General Hospital Corporation (Boston, MA) "MGH"
#
# Terms and conditions for use, reproduction, distribution and contribution
# are found in the 'FreeSurfer Software License Agreement' contained
# in the file 'LICENSE' found in the FreeSurfer distribution, and here:
#
# https://surfer.nmr.mgh.harvard.edu/fswiki/FreeSurferSoftwareLicense
#
# Reporting: freesurfer@nmr.mgh.harvard.edu
#
#

### --  use NMR Center VTK package -- ####

if ( $?FREESURFER_HOME && ! $?MY_VTK_HOME ) then
  if ( -e $FREESURFER_HOME/lib/vtk ) then
    set FS_VTK_HOME=$FREESURFER_HOME/lib/vtk
  else if ( -e /usr/pubsw/packages/vtk/current ) then
    set FS_VTK_HOME=/usr/pubsw/packages/vtk/current
  endif
else if ( $?MY_VTK_HOME ) then
  set FS_VTK_HOME=$MY_VTK_HOME
else if ( -e /usr/pubsw/packages/vtk/current ) then
  set FS_VTK_HOME=/usr/pubsw/packages/vtk/current
endif

if ( $?FS_VTK_HOME ) then

  if ( -e $FS_VTK_HOME/bin ) then
    setenv PATH "$FS_VTK_HOME/bin":"$PATH"
  endif

  if ( -e $FS_VTK_HOME/lib ) then

    setenv VTKLIBPATH $FS_VTK_HOME/lib
    if ( -e $FS_VTK_HOME/lib/vtk-5.10 ) then
      setenv VTKLIBPATH $FS_VTK_HOME/lib/vtk-5.10
    else
      if ( -e $FS_VTK_HOME/lib/vtk-5.6 ) then
        setenv VTKLIBPATH $FS_VTK_HOME/lib/vtk-5.6
      else
        if ( -e $FS_VTK_HOME/lib/vtk-5.4 ) then
          setenv VTKLIBPATH $FS_VTK_HOME/lib/vtk-5.4
        else
          if ( -e $FS_VTK_HOME/lib/vtk-5.3 ) then
            setenv VTKLIBPATH $FS_VTK_HOME/lib/vtk-5.3
          else
            if ( -e $FS_VTK_HOME/lib/vtk-5.2 ) then
              setenv VTKLIBPATH $FS_VTK_HOME/lib/vtk-5.2
            endif
          endif
        endif
      endif
    endif

    if(! $?LD_LIBRARY_PATH ) then
      setenv LD_LIBRARY_PATH $VTKLIBPATH
    else
      setenv LD_LIBRARY_PATH "$VTKLIBPATH":"$LD_LIBRARY_PATH"
    endif
    if(! $?DYLD_LIBRARY_PATH ) then
      setenv DYLD_LIBRARY_PATH $VTKLIBPATH
    else
      setenv DYLD_LIBRARY_PATH "$VTKLIBPATH":"$DYLD_LIBRARY_PATH"
    endif

    # if vtk python wrappers were built...
    if ( -e $FS_VTK_HOME/lib/python3.0/site-packages) then
      setenv VTKPYPATH $FS_VTK_HOME/lib/python3.0/site-packages
    else if ( -e $FS_VTK_HOME/lib/python2.6/site-packages) then
      setenv VTKPYPATH $FS_VTK_HOME/lib/python2.6/site-packages
    else if ( -e $FS_VTK_HOME/lib/python2.5/site-packages) then
      setenv VTKPYPATH $FS_VTK_HOME/lib/python2.5/site-packages
    else if ( -e $FS_VTK_HOME/lib/python2.4/site-packages) then
      setenv VTKPYPATH $FS_VTK_HOME/lib/python2.4/site-packages
    else if ( -e $FS_VTK_HOME/lib/python2.3/site-packages) then
      setenv VTKPYPATH $FS_VTK_HOME/lib/python2.3/site-packages
    else if ( -e $FS_VTK_HOME/lib/python2.2/site-packages) then
      setenv VTKPYPATH $FS_VTK_HOME/lib/python2.2/site-packages
    endif
    if ( $?VTKPYPATH ) then
      if(! $?PYTHONPATH ) then
        setenv PYTHONPATH $VTKPYPATH 
      else
        setenv PYTHONPATH "$VTKPYPATH":"$PYTHONPATH"
      endif
    endif

  endif

  # default: assume platform has VTK libs installed somewhere
endif
