#! /bin/tcsh -f

#set echo=1

if ( ! $?SUBJECTS_DIR ) then
    echo "SUBJECTS_DIR is not declared"
    exit 1
endif

set subj=$1
if ( ! -e $SUBJECTS_DIR/$subj/surf ) then
    echo "$SUBJECTS_DIR/$subj/surf does not exist"
    echo ""
    echo "USAGE: vno_match_check <subjid>"
    echo ""
    echo "Checks that all surface and surface data files for subject <subjid>"
    echo "have the same number of vertices."
    exit 1
endif

cd $SUBJECTS_DIR/$subj/surf

foreach hemi ( rh lh )
    
    # get number of vertices of the orig surface
    if ("x$2" != "x") echo "Checking $hemi.orig..."
    set vno_orig=`mris_info $hemi.orig \
        |& grep "num vertices:" \
        |& awk '{print $3}'`
    if ("x$vno_orig" == "x") then
        echo "Error running mris_info"
        exit 1
    endif

    # and check that all the other surfaces have this same number
    foreach surf ( white pial inflated )

        if ("x$2" != "x") echo "Checking $hemi.$surf..."
        set vno=`mris_info $hemi.$surf \
            |& grep "num vertices:" \
            |& awk '{print $3}'`
        if ("x$vno" == "x") then
            echo "Error running mris_info"
            exit 1
        endif

        if ( "$vno_orig" != "$vno" ) then
            echo "ERROR: $subj/surf/$hemi.orig has $vno_orig vertices"
            echo "while  $subj/surf/$hemi.$surf has $vno"
            exit 1
        endif

    end

    # now check that the curvature files have the correct number of vertices
    foreach curv ( curv sulc area thickness )

        if ("x$2" != "x") echo "Checking $hemi.$curv..."
        mris_info $hemi.orig --c $hemi.$curv >& /tmp/mris_info.log
        if ($status) then
            grep ERROR /tmp/mris_info.log
            exit 1
        endif

    end

end

echo "Pass: all surfaces and surface data for subject $subj have the same number of vertices."
