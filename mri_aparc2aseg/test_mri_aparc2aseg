#!/bin/tcsh -f

#
# test_mri_aparc2aseg
#
# execute mri_aparc2aseg and check results against expected results
#
# Original Author: Nick Schmansky
# CVS Revision Info:
#    $Author: nicks $
#    $Date: 2007/01/16 23:46:36 $
#    $Revision: 1.3 $
#
# Copyright (C) 2002-2007,
# The General Hospital Corporation (Boston, MA). 
# All rights reserved.
#
# Distribution, usage and copying of this software is covered under the
# terms found in the License Agreement file named 'COPYING' found in the
# FreeSurfer source code root directory, and duplicated here:
# https://surfer.nmr.mgh.harvard.edu/fswiki/FreeSurferOpenSourceLicense
#
# General inquiries: freesurfer@nmr.mgh.harvard.edu
# Bug reports: analysis-bugs@nmr.mgh.harvard.edu
#

set VERSION='$Id: test_mri_aparc2aseg,v 1.3 2007/01/16 23:46:36 nicks Exp $'

umask 002

setenv SUBJECTS_DIR $PWD

echo "gunzip -c testdata.tar.gz | tar xvf -"
gunzip -c testdata.tar.gz | tar xvf -
if ($status != 0) then
  echo "test_mri_aparc2aseg FAILED"
  exit 1
endif

set cmd=(./mri_aparc2aseg --s testdata --ribbon)
echo ""
echo $cmd
echo "Output is directed to file 'mri_aparc2aseg.out.txt'..."
$cmd > mri_aparc2aseg.out.txt
if ($status != 0) then
  echo "test_mri_aparc2aseg FAILED"
  exit 1
endif

set TEST_FILES=( aparc+aseg.mgz )
foreach tstvol ($TEST_FILES)
  # now compare expected with observed data
  set REF_VOL  = testdata/mri/reference_aparc+aseg.mgz
  set TST_VOL  = testdata/mri/$tstvol
  set DIFF_VOL = $PWD/mri_diff-$tstvol
  set MRIDIFFF = $PWD/mri_diff-$tstvol.txt
  set cmd=(../mri_diff/mri_diff --debug \
            --thresh 0.0 \
            --log $MRIDIFFF \
            $REF_VOL $TST_VOL --diff $DIFF_VOL);
  echo ""
  echo $cmd
  $cmd
  set diff_status=$status
  if ($diff_status != 0) then
    echo "test_aparc2aseg $cmd FAILED (exit status=$diff_status)"
    exit 1
  endif
end

#
# cleanup
#
cd ..
rm -Rf testdata

echo ""
echo "test_mri_aparc2aseg passed all tests"
exit 0
