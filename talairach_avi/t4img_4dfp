#!/bin/csh

set rcsid = 't4img_4dfp @FS_VERSION@'

@ debug = 0

if ($#argv < 2) then
	echo "Usage:	t4img_4dfp <t4file> <imgfile> [outfile] [options]"
	echo " e.g.,	t4img_4dfp vce1_mprS_to_711-2B_t4	vce1_mprS.4dfp.img -O222"
	echo "   or,	t4img_4dfp vce1_mprS_to_711-2B_t4 	vce1_mprS vce_mprS_711-2B -O222"
	echo "   or,	t4img_4dfp none				vce1_mprS vce1_mprS_222 -O222"
	echo "N.B.:	.4dfp.img filename extensions are optional"
	echo "N.B.:	default output filename = <imgfile>t"
	echo "N.B.:	options must follow the filename arguments"
	exit 1
endif

set srgv =	($argv[1-])
set t4file =	$argv[1]; shift
set imgfile =	$argv[1]; shift
if ($t4file == "none") set t4file = ""

if ($debug) echo n=$#argv
set outfile = ""
if ($#argv) set outfile = `echo $argv[1] | awk '($1 !~ /^\-/){print $1}'`
if ($outfile != "") then
	shift
else
	set outfile =	$imgfile:r
	set outfile =	$outfile:r
	set outfile =	$outfile:t
	set outfile =	$outfile"t"
endif

echo $imgfile"	t4="$t4file >! $outfile.lst
echo	t4imgs_4dfp $outfile.lst $outfile $argv
	t4imgs_4dfp $outfile.lst $outfile $argv
if ($status) exit $status

mv						$outfile.4dfp.img.rec temp$$
echo rec $outfile.4dfp.img `date` `whoami` >!	$outfile.4dfp.img.rec
echo $0 $srgv >>				$outfile.4dfp.img.rec
echo $rcsid >>					$outfile.4dfp.img.rec
set n = (`wc temp$$`); @ n = $n[1] - 3;
tail +3 temp$$ | head -$n >>			$outfile.4dfp.img.rec
echo endrec `date` `whoami` >>			$outfile.4dfp.img.rec
/bin/rm temp$$ $outfile.lst

exit
