#!/bin/tcsh -f

#
# test_mri_ca_register
#
# execute mri_ca_register and check results against expected results
#

set VERSION='$Id: test_mris_register,v 1.0 2018/01/24 15:54:00 bbrett Exp $'

umask 002

if ( $?SKIP_MRIS_REGISTER_TEST ) then
  exit 0
endif

#
# extract testing data
#
gunzip -c testdata.tar.gz | tar xvf -

#
# run mris_register using typical input file formats
#

cd testdata

setenv FREESURFER_HOME ../../distribution
setenv SUBJECTS_DIR ""

setenv OMP_NUM_THREADS 8

# modified (shortened) usage in recon-all: 
set cmd=(../mris_register \
        -curv \
    -rusage rusage.mris_register.lh.dat \
    lh.sphere \
    lh.folding.atlas.acfb40.noaparc.i12.2016-08-02.tif \
    lh.sphere.reg)

echo ""
echo $cmd
$cmd
if ($status != 0) then
  echo "mris_register FAILED"
  exit 1
endif

#
# compare expected results with actual (produced) results
#

set REF_VOL = ref_lh.sphere.reg
set TST_VOL = lh.sphere.reg

hd -s 96 -n 550000 < $REF_VOL > ${REF_VOL}.hd.txt
hd -s 96 -n 550000 < $TST_VOL > ${TST_VOL}.hd.txt

set cmd=(diff -q ${REF_VOL}.hd.txt ${TST_VOL}.hd.txt );
echo ""
echo $cmd
$cmd
set diff_status=$status
if ($diff_status != 0) then
  echo "$cmd FAILED (exit status=$diff_status)"
  exit 1
endif

echo ""
echo ""
echo ""

#
# cleanup
#
cd ..
rm -Rf testdata

echo ""
echo "test_mris_register passed all tests"
exit 0
