#! /bin/csh -f

#
# ksgrinder-sess
#
# Original Author: Doug Greve
#
# Copyright Â© 2021
# The General Hospital Corporation (Boston, MA). 
# All rights reserved.
#
# Distribution, usage and copying of this software is covered under the
# terms found in the License Agreement file named 'COPYING' found in the
# FreeSurfer source code root directory, and duplicated here:
# https://surfer.nmr.mgh.harvard.edu/fswiki/FreeSurferOpenSourceLicense
#
# General inquiries: freesurfer@nmr.mgh.harvard.edu
# Bug reports: analysis-bugs@nmr.mgh.harvard.edu
#


set os = `uname -s`;
if($os != "SunOS") then
  echo "ERROR: must be on SunOS to run ksgrinder"
  exit 1;
endif

set VERSION = 'ksgrinder-sess @FS_VERSION@';

set inputargs = ($argv);

set contrast    = ();
set analysis    = ();
set ctlcondlist = ();
set actcondlist = ();
set delay       = 0;
set umaskarg    = ();
set SessList     = ();
set nolog = 0;

set n = `echo $argv | grep version | wc -l` 
if($n != 0) then
  echo $VERSION
  exit 0;
endif

if($#argv == 0) then
  goto usage_exit;
  exit 1;
endif

source $FREESURFER_HOME/sources.csh

goto parse_args;
parse_args_return:

set SessList = `getsesspath $inputargs`;
if($status) then
  echo "$SessList"
  exit 1;
endif

goto check_params;
check_params_return:

## Get functional subdirectory from the info file ##
set infofile = $analysis/analysis.info
if(! -e $infofile) then
  echo "ERROR: cannot find $infofile"
  exit 1;
endif
set fsd = `cat $analysis/analysis.info | awk '{if($1 == "fsd") print $2}'`;
set parname   = `cat $infofile | awk '{if($1 == "parname") print $2}'`;
set runlistfile = `cat $infofile | awk '{if($1 == "runlistfile") print $2}'`;
set funcstem  = `cat $infofile | awk '{if($1 == "funcstem") print $2}'`;

## Look for nolog option ##
set n = `echo $inputargs | grep nolog | wc -l` 
if($n != 0) set nolog = 1;

##### Create a log file ######
if(! $nolog) then
  set logdir = `pwd`/log;
  mkdir -p $logdir
  if(! -e $logdir) then
    echo "ERROR: could not create $logdir"
    exit 1;
  endif
  set LF = $logdir/ksgrinder-sess.log
  if(-e $LF) mv $LF $LF.old
else
  echo "No log file"
  set LF = /dev/null
endif

echo "ksgrinder-sess Logfile is $LF"

echo "ksgrinder-sess log file" >> $LF
echo $VERSION >> $LF
id            >> $LF
pwd           >> $LF
echo "$0"     >> $LF
echo $inputargs  >> $LF
uname -a      >> $LF
date          >> $LF
which ksgrinder >> $LF

foreach sess ($SessList)
  echo "-------------------------------------------------------"
  set sessid = `basename $sess`;
  echo "Session: $sess "

  pushd $sess/$fsd > /dev/null

  if($#runlistfile == 0) then
    set RunList = `getrunlist .`;
    if($status || $#RunList == 0) then
      echo "ERROR: $sess/$fsd has no runs"  |& tee -a $LF
      exit 1;
    endif
  else
    if(! -e $runlistfile) then
      echo "ERROR: ($sessbase) could not find runlistfile $runlistfile"
      exit 1;
    endif
    set RunList = (`cat $runlistfile`);
  endif
  echo "INFO ($sessid): RunList = $RunList"

  set parfile = $RunList[1]/$parname
  if(! -e $parfile ) then
    echo "ERROR ($sessid): cannot find $parfile"
    exit 1;
  endif

  # Compute the vertial average #
  set vertavgstem = /tmp/$sessid
  set inverts = ();
  foreach run ($RunList)  
    set inverts = ($inverts -i $run/$funcstem)
  end
  echo "--- Computing vertical average -----"
  set cmd = (avgraw $inverts -o $vertavgstem)
  pwd
  echo $cmd
  $cmd
  if($status) exit 1;

  set outstem = $analysis/$contrast/sig

  set cmd = (ksgrinder -i $vertavgstem -o $outstem -parfile $parfile)
  set cmd = ($cmd $actcondlist $ctlcondlist -delay $delay)

  echo "-------------------------------------------------"
  pwd
  echo $cmd
  echo "-------------------------------------------------"
  $cmd
  set ksgstat = $status;

  # Delete the vertical average #
  rm $vertavgstem*

  if($ksgstat) then
    echo "ERROR ($sessid): ksgrinder failed"
    exit 1;
  endif

exit 1;

end # loop over SessList #

date | tee -a $LF
echo " "
echo "ksgrinder-sess completed SUCCESSFULLY" | tee -a $LF
echo " "

exit 0;
###############################################

############--------------##################
parse_args:
set cmdline = ($argv);
while( $#argv != 0 )

  set flag = $argv[1]; shift;
  
  switch($flag)

    case "-contrast":
      if ( $#argv == 0) goto arg1err;
      set contrast = $argv[1]; shift;
      breaksw

    case "-analysis":
      if ( $#argv == 0) goto arg1err;
      set analysis = $argv[1]; shift;
      breaksw

    case "-delay":
      if ( $#argv == 0) goto arg1err;
      set delay = $argv[1]; shift;
      breaksw

    case "-a":
      if ( $#argv == 0) goto arg1err;
      set actcondlist = ($actcondlist -a $argv[1]); shift;
      breaksw

    case "-c":
      if ( $#argv == 0) goto arg1err;
      set ctlcondlist = ($ctlcondlist -c $argv[1]); shift;
      breaksw

    case "-umask":
      if ( $#argv == 0) goto arg1err;
      set umaskarg = "-umask $argv[1]";
      umask $argv[1]; shift;
      breaksw

    case "-verbose":
      set verbose = 1;
      breaksw

    case "-echo":
      set echo = 1;
      breaksw

    case "-debug":
      set verbose = 1;
      set echo = 1;
      breaksw

    case "-nolog"
      breaksw

    case "-cwd"
      set IsSess = 1;  # otherwise, ignore getsesspath arguments 
      breaksw

    case "-s":
    case "-sf":
    case "-df":
    case "-d":
    case "-g":
      set IsSess = 1;  # otherwise, ignore getsesspath arguments 
      shift;
      breaksw

    default:
      echo ERROR: Flag $flag unrecognized. 
      echo $cmdline
      exit 1
      breaksw
  endsw

end

goto parse_args_return;
############--------------##################

############--------------##################
check_params:

  if ($#contrast == 0) then
     echo "ERROR: no contrast specified"
     exit 1
  endif

  if ($#analysis == 0) then
     echo "ERROR: no analysis specified"
     exit 1
  endif

  if($#actcondlist == 0) then
    echo "ERROR: no active conditions specified"
    exit 1;
  endif

  if($#ctlcondlist == 0) then
    echo "ERROR: no control conditions specified"
    exit 1;
  endif

goto check_params_return;
############--------------##################

############--------------##################
arg1err:
  echo "ERROR: flag $flag requires one argument"
  exit 1
############--------------##################

############--------------##################
usage_exit:
  echo "USAGE: ksgrinder-sess"
  echo "Options:";
  echo "   -analysis  analysisname : name of session-level functional analysis";
  echo "   -contrast  contrastname : contrast name"
  echo "   -a condno <-a condno>   : active conditions"
  echo "   -c condno <-c condno>   : control conditions"
  echo "   -delay    ntrs          : hemodynamic delay in TRs"
  echo "   -sf sessidfile  ..."
  echo "   -df srchdirfile ..."
  echo "   -s  sessid      ..."
  echo "   -d  srchdir     ..."
  echo "   -scriptonly    : don't run, just generate a script"
  echo "   -version       : print version and exit"
  echo "   -umask umask   : set unix file permission mask"
exit 1;
