#!/bin/tcsh -f

#
# test_mris_ca_label
#
# execute mris_ca_label and check results against expected results
#
# Original Author: Nick Schmansky
# CVS Revision Info:
#    $Author: nicks $
#    $Date: 2012/12/12 21:18:23 $
#    $Revision: 1.5 $
#
# Copyright Â© 2011 The General Hospital Corporation (Boston, MA) "MGH"
#
# Terms and conditions for use, reproduction, distribution and contribution
# are found in the 'FreeSurfer Software License Agreement' contained
# in the file 'LICENSE' found in the FreeSurfer distribution, and here:
#
# https://surfer.nmr.mgh.harvard.edu/fswiki/FreeSurferSoftwareLicense
#
# Reporting: freesurfer@nmr.mgh.harvard.edu
#
# General inquiries: freesurfer@nmr.mgh.harvard.edu
#

set VERSION='$Id: test_mris_ca_label,v 1.5 2012/12/12 21:18:23 nicks Exp $'

umask 002

# backdoor bypass:
if ( $?SKIP_MRIS_CA_LABEL_TEST ) then
  echo "skipping test_mris_ca_label"
  exit 77
endif

# fake-out an installation:
setenv SUBJECTS_DIR $PWD
setenv FREESURFER_HOME ../distribution

set LOG=(test_mris_ca_label.log)

if ( -e testdata) then
  echo "rm -Rf testdata"
  rm -Rf testdata
endif

echo "gunzip -c testdata.tar.gz | tar xvf -"
gunzip -c testdata.tar.gz | tar xvf -

#
# create Desikan parcellation
#
set cmd=(./mris_ca_label -l testdata/label/lh.cortex.label \
    -aseg testdata/mri/aseg.mgz \
    -seed 1234 \
    testdata \
    lh \
    testdata/surf/lh.sphere.reg \
    ../distribution/average/lh.curvature.buckner40.filled.desikan_killiany.2010-03-25.gcs \
    testdata/label/lh.aparc.annot)
echo ""
echo $cmd
echo "Output is directed to file '$LOG'..."
if (-e $LOG) rm -f $LOG
$cmd |& tee -a $LOG
if ($status != 0) then
  echo "test_mris_ca_label FAILED"
  exit 1
endif

#
# compare it to reference
#
set cmd=(../mris_diff/mris_diff --maxerrs 1000 \
    --s1 testdata --s2 testdata --hemi lh \
    --aparc aparc --aparc2 aparc.reference)
echo ""
echo $cmd
$cmd |& tee -a $LOG
set diff_status=$status
if ($diff_status != 0) then
  echo "test_mris_ca_label $cmd FAILED (exit status=$diff_status)"
  exit 1
endif


#
# create Destrieux parcellation
#
set cmd=(./mris_ca_label -l testdata/label/lh.cortex.label \
    -aseg testdata/mri/aseg.mgz \
    -seed 1234 \
    testdata \
    lh \
    testdata/surf/lh.sphere.reg \
    ../distribution/average/lh.destrieux.simple.2009-07-29.gcs \
    testdata/label/lh.aparc.a2009s.annot)
echo ""
echo $cmd
echo "Output is directed to file '$LOG'..."
$cmd |& tee -a $LOG
if ($status != 0) then
  echo "test_mris_ca_label FAILED"
  exit 1
endif

#
# compare it to reference
#
set cmd=(../mris_diff/mris_diff --maxerrs 1000 \
    --s1 testdata --s2 testdata --hemi lh \
    --aparc aparc.a2009s --aparc2 aparc.a2009s.reference)
echo ""
echo $cmd
$cmd |& tee -a $LOG
set diff_status=$status
if ($diff_status != 0) then
  echo "test_mris_ca_label $cmd FAILED (exit status=$diff_status)"
  exit 1
endif


#
# cleanup
#
cd $SUBJECTS_DIR
rm -Rf testdata

echo ""
echo "test_mris_ca_label passed all tests"
exit 0
