#!/bin/tcsh -f

#
# test_mris_volmask
#
# execute mris_volmask and check results against expected results
#
# Original Author: Nick Schmansky
# CVS Revision Info:
#    $Author: zkaufman $
#    $Date: 2016/11/22 17:29:57 $
#    $Revision: 1.9 $
#
# Copyright Â© 2011 The General Hospital Corporation (Boston, MA) "MGH"
#
# Terms and conditions for use, reproduction, distribution and contribution
# are found in the 'FreeSurfer Software License Agreement' contained
# in the file 'LICENSE' found in the FreeSurfer distribution, and here:
#
# https://surfer.nmr.mgh.harvard.edu/fswiki/FreeSurferSoftwareLicense
#
# Reporting: freesurfer@nmr.mgh.harvard.edu
#
# General inquiries: freesurfer@nmr.mgh.harvard.edu
#

set VERSION='$Id: test_mris_volmask,v 1.9 2016/11/22 17:29:57 zkaufman Exp $'

umask 002

# check for an enviro var to skip
if ( $?SKIP_MRIS_VOLMASK_TEST ) exit 77

#
# extract testing data, use existing test data from another test setup
#
rm -Rf testdata2 >& /dev/null
gunzip -c ./testdata2.tar.gz | tar xvf -

#
# run mris_volmask
#

setenv SUBJECTS_DIR $PWD
# no longer necessary: source ../scripts/vtk_setup
set cmd=(./mris_volmask  --label_left_white   2 --label_left_ribbon   3 \
    --label_right_white 41 --label_right_ribbon 42 \
    --save_ribbon testdata2)
echo ""
echo $cmd
echo $cmd >& test_mris_volmask.log
echo ""
$cmd >>& test_mris_volmask.log
if ($status != 0) then
  echo "mris_volmask FAILED"
  exit 1
endif

#
# compare expected results with actual (produced) results
#
compare:
set MRIDIFFF=(testdata2/mri/mri_diff-ribbon.mgz.log)
set REF_VOL=(testdata2/mri/reference_ribbon.mgz)
set TST_VOL=(testdata2/mri/ribbon.mgz)
set DIFF_VOL=(testdata2/mri/mri_diff-ribbon.mgz)
set cmd=(../mri_diff/mri_diff --debug \
          --thresh 0.0 \
          --log $MRIDIFFF \
          $REF_VOL $TST_VOL --diff $DIFF_VOL);
echo ""
echo $cmd
echo $cmd >>& test_mris_volmask.log
$cmd >>& test_mris_volmask.log
set diff_status=$status
if ($diff_status != 0) then
  echo "$cmd FAILED (exit status=$diff_status)"
  exit 1
endif

#
# cleanup
#
end:
rm -Rf testdata2

echo ""
echo "test_mris_volmask passed all tests"
exit 0
