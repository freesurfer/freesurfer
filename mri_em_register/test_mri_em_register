#!/bin/tcsh -f

#
# test_mri_em_register
#
# execute mri_em_register and check results against expected results
#
# Original Author: Nick Schmansky
# CVS Revision Info:
#    $Author: zkaufman $
#    $Date: 2016/04/30 16:10:38 $
#    $Revision: 1.5 $
#
# Copyright Â© 2014 The General Hospital Corporation (Boston, MA) "MGH"
#
# Terms and conditions for use, reproduction, distribution and contribution
# are found in the 'FreeSurfer Software License Agreement' contained
# in the file 'LICENSE' found in the FreeSurfer distribution, and here:
#
# https://surfer.nmr.mgh.harvard.edu/fswiki/FreeSurferSoftwareLicense
#
# Reporting: freesurfer@nmr.mgh.harvard.edu
#
# General inquiries: freesurfer@nmr.mgh.harvard.edu
#

set VERSION='$Id: test_mri_em_register,v 1.5 2016/04/30 16:10:38 zkaufman Exp $'

umask 002

#
# extract testing data
#
gunzip -c testdata.tar.gz | tar xvf -

#
# run mri_em_register using typical input file formats,
# first with one thread then with 8 threads.
#

cd testdata

set log = (../test_mri_em_register.log)
if (-e $log) rm -f $log 

set threads=( 1 8 )
if ( $?SKIP_MRI_EMREG_1CPU_TEST ) set threads=( 8 )

foreach num ($threads)

    setenv OMP_NUM_THREADS $num
    set cmd=(../mri_em_register \
        -uns 3 \
        -mask brainmask.mgz \
        nu.mgz \
        ../../distribution/average/RB_all_2014-08-21.gca \
        talairach.${num}cpu.lta)
    echo "\n\n $cmd \n\n" |& tee -a $log
    $cmd |& tee -a $log
    if ($status != 0) then
        echo "mri_em_register FAILED" |& tee -a $log
    exit 1
    endif

end

echo "" |& tee -a $log
echo "" |& tee -a $log
echo "" |& tee -a $log

#
# compare expected results with actual (produced) results
#

foreach num ($threads)

  set cmd=(../../mri_robust_register/lta_diff \
    talairach.lta talairach.${num}cpu.lta);
  echo "\n\n $cmd \n\n" |& tee -a $log
  set result = (`$cmd |& tee -a $log`)
  if ($result != 0) then
    echo "$cmd FAILED (RMS=$result)" |& tee -a $log
    exit 1
  endif

end

#
# cleanup
#
cd ..
rm -Rf testdata

echo "" |& tee -a $log
echo "test_mri_em_register passed all tests" |& tee -a $log
exit 0
