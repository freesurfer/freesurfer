#!/bin/tcsh -f
#
# unit test for: mriBuildVoronoiDiagramFloat
#     - this script provides source and reference data for the testing binary
#     - ToDo: completely hardcode the test inside 
#             test_mriBuildVoronoiDiagramFloat.cpp
#             and not rely on external data files
#


umask 002

# extract testing data:
gunzip -c testdata.tar.gz | tar xvf -


#
# run test_mriBuildVoronoiDiagramFloat with the source and reference files:
#

cd testdata

setenv FREESURFER_HOME ../../distribution
setenv SUBJECTS_DIR ""


set cmd=(../test_mriBuildVoronoiDiagramFloat src.mgz ctrl.mgz dst.mgz)
echo
$cmd
if ($status != 0) then
  echo "test_mriBuildVoronoiDiagramFloat FAILED"
  exit 1
endif

echo
echo
echo


#
# compare expected results with produced results:
#

set REF_VOL = ref.mgz
set TST_VOL = dst.mgz

set DIFF_VOL = $PWD/mri_diff-$TST_VOL
set MRIDIFFF = $PWD/mri_diff-$TST_VOL.txt

set cmd=(../../../../mri_diff/mri_diff --debug \
          --thresh 0.0 \
          --res-thresh 0.000001 \
          --geo-thresh 0.000008 \
          --log $MRIDIFFF \
          $REF_VOL $TST_VOL --diff $DIFF_VOL);
echo
echo $cmd
$cmd
set diff_status=$status
if ($diff_status != 0) then
  echo "$cmd FAILED (exit status=$diff_status)"
  exit 1
endif


# cleanup:
cd ..
rm -rf testdata

echo
echo "mriBuildVoronoiDiagramFloat passed test"
exit 0
