#!/bin/tcsh -f

#
# test_mri_ca_normalize
#
# execute mri_ca_normalize and check results against expected results
#
# Original Author: Nick Schmansky
# CVS Revision Info:
#    $Author: zkaufman $
#    $Date: 2016/07/06 19:38:11 $
#    $Revision: 1.8.2.1 $
#
# Copyright Â© 2011 The General Hospital Corporation (Boston, MA) "MGH"
#
# Terms and conditions for use, reproduction, distribution and contribution
# are found in the 'FreeSurfer Software License Agreement' contained
# in the file 'LICENSE' found in the FreeSurfer distribution, and here:
#
# https://surfer.nmr.mgh.harvard.edu/fswiki/FreeSurferSoftwareLicense
#
# Reporting: freesurfer@nmr.mgh.harvard.edu
#

set VERSION='$Id: test_mri_ca_normalize,v 1.8.2.1 2016/07/06 19:38:11 zkaufman Exp $'

umask 002

#
# extract testing data
#
gunzip -c testdata.tar.gz | tar xvf -

#
# run mri_ca_normalize using typical input args (and the same as those used
# to produce the expected results)
#
if (-e norm.mgz) rm -f norm.mgz
if (-e ctrl_pts.mgz) rm -f ctrl_pts.mgz
set cmd=(./mri_ca_normalize \
    -mask brainmask.mgz \
    -c ctrl_pts.mgz \
    nu.mgz \
    ../distribution/average/RB_all_2016-05-10.vc700.gca \
    talairach.lta \
    norm.mgz)
echo $cmd
$cmd
if ($status != 0) then
  echo "mri_ca_normalize FAILED"
  exit 1
endif

#
# compare expected results with actual (produced) results
#
set TST_VOL=norm.mgz
set REF_VOL=reference_norm.mgz
if ("`uname -p`" == "powerpc") set REF_VOL=reference_ppc_norm.mgz
set DIFF_VOL = $PWD/mri_diff-$TST_VOL
set MRIDIFFF = $PWD/mri_diff-$TST_VOL.txt
set THRESHOLD = 0
set cmd=(../mri_diff/mri_diff --debug --thresh $THRESHOLD \
      --log $MRIDIFFF $REF_VOL $TST_VOL --diff $DIFF_VOL);
echo $cmd
$cmd
set diff_status=$status
if ($diff_status != 0) then
  echo "$cmd FAILED (exit status=$diff_status)"
  exit 1
endif

set TST_VOL=ctrl_pts.mgz
set REF_VOL=reference_ctrl_pts.mgz
set DIFF_VOL = $PWD/mri_diff-$TST_VOL
set MRIDIFFF = $PWD/mri_diff-$TST_VOL.txt
set THRESHOLD = 0
set cmd=(../mri_diff/mri_diff --debug --thresh $THRESHOLD \
      --log $MRIDIFFF $REF_VOL $TST_VOL --diff $DIFF_VOL);
echo $cmd
$cmd
set diff_status=$status
if ($diff_status != 0) then
  echo "$cmd FAILED (exit status=$diff_status)"
  exit 1
endif

echo ""
echo "test_mri_ca_normalize passed all tests"
exit 0
