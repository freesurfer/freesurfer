#!/bin/tcsh -f

umask 002

if ( $?SKIP_MRIS_FIX_TOPOLOGY_TEST ) then
  exit 0
endif

set passed = 1

foreach niters ( 2 -1 )
  if ($niters != -1) then
    set NITERS_OPTION = "-niters $niters"
    set NITERS_EXTENSION = _n$niters
  else
    set NITERS_OPTION = 
    set NITERS_EXTENSION = _unlimited
  endif
  
  foreach threads ( 4 8 )

    # extract testing data
    rm -rf testdata
    gunzip -c testdata.tar.gz | tar xvf -

    cd testdata
    cp subjects/bert/surf/lh.orig{.before,}

    setenv FREESURFER_HOME ../../distribution
    setenv SUBJECTS_DIR `pwd`/subjects
    setenv OMP_NUM_THREADS $threads
    echo "testing with $threads thread(s)"

    setenv FREESURFER_REPLACEMENT_FOR_CREATION_TIME_STRING "Sun Jan 11 11:11:11 ZONE 2011"

    # ---- TEST 1 ----

    # run mris_make_surfaces using typical input

    if (1) then

      set cmd=(../mris_fix_topology \
              $NITERS_OPTION \
              -mgz \
              -sphere qsphere.nofix \
              -ga -seed 1234 bert lh)

      echo ""
      echo $cmd
      $cmd
      if ($status != 0) then
        echo "mris_fix_topology FAILED"
        set passed = 0
      else
        # compare expected results with actual (produced) results

        set TST = subjects/bert/surf/lh.orig

    	mv $TST   $TST${NITERS_EXTENSION}
	set TST = $TST${NITERS_EXTENSION}

        set REF = ${TST}.REF
        set cmd=(../../mris_diff/mris_diff --debug $REF $TST );

        echo ""
        echo $cmd
        $cmd
        set diff_status=$status
        if ($diff_status != 0) then
          echo "$cmd FAILED (exit status=$diff_status)"
          set passed = 0
        endif
      endif

    endif


    # cleanup, saving results for making new REF files

    cd ..

    rm -rf testdata${NITERS_EXTENSION}
    mv testdata{,${NITERS_EXTENSION}}    

  end
end

if ($passed != 0) then
  echo ""
  echo "test_mris_fix_topology passed all tests"
  exit 0
else
  echo ""
  echo "test_mris_fix_topology failed some tests"
  exit 1
endif
