#!/bin/tcsh -ef

##
## For infants between 0-2yrs of age!
##

set inputargs = ($argv);
set VERSION = 'infant_recon_all @FS_VERSION@';

set checkresults        = 0 # after each processing step FreeView is called to visualize the results
set nolog               = 0 # keep logging the processing steps
set cleanup             = 1 

set fsdirset            = 0
set maskedinput         = 0
set segfileinput        = 0
set inputfile           = 0 # inputfilename is assumed to be mprage.mgz, by default

# default is using all CNY-BCH templates
set defaultinfants      = 1 # CNY-BCH training set
set newborns            = 0
set oneyearolds         = 0
set trainingsetsize     = 4 # set, by default, only for full  CNY-BCH templates
set subjage             = -10

set force               = 0 # force overwrite; delete all previously generated files / directories

set withGMWM            = 1 # just testing if GM/WM is present
set forcegmwm           = 0 # if set, check that at least one training subject has a GMWM segmentation!
set forcegmwm2          = 1 # if set, force to choose at least one training subject has a GMWM segmentation!

set forceskullstrip     = 0

set avoidtraining       = 0
set setksize            = 0

set computeCCseg        = 0

set dostats             = 1

set intnormFSL          = 0

set useMI               = 0
set PrintHelp           = 0

# don't use GPU by default
set gpuid = -1
set usegpu = 0

# non-linear registration choice
# set regtype   = NIFTYREG # by default instead of dramms -- already set in recon_params.csh
set usedramms = 0      # still here for backwards compatibility

#  --t2 --t2file
set uset2 = 0
set t2filename = 0

setenv INFANTSCRIPTDIR `dirname $0`
echo InfantReconAll scripts DIR $INFANTSCRIPTDIR

source $INFANTSCRIPTDIR/set_infant_recon_params.csh
set TEMPLATE_SUBJECTS = ($CNYBCH_SUBJECTS) # the full CNYBCH training set
set TEMPLATE_AGES     = ($CNYBCH_AGES)
setenv TEMPLATE_SUBJECTS_DIR  $CNYBCH_TEMPLATE_SUBJECTS_DIR 

## read input
set cmdline = ($argv);

if($#argv == 0) goto usage_exit;

while( $#argv != 0 )
  set flag = $argv[1]; shift;
  switch($flag)
    case "-h"
    case "-u"
    case "-usage"
    case "--usage"
    case "-help"
    case "--help"
      set PrintHelp = 1;
      goto usage_exit;
      breaksw
    case "--subject":
    case "--s":
      if ( $#argv < 1) goto arg1err;
      set subj = $argv[1]; shift;
      breaksw
    case "--age":
      if ( $#argv < 1) goto arg1err;
      set subjage = $argv[1]; shift;
      breaksw
    case "--kneigh":
      if ( $#argv < 1) goto arg1err;
      set setksize = 1;
      set trainingsetsize = $argv[1]; shift;
      breaksw
    case "--outdir":
      if ( $#argv < 1) goto arg1err;
      set FS_DIR = $argv[1]; shift;
      # set the "true" FS_DIR path
      mkdir -p $FS_DIR
      pushd $FS_DIR > /dev/null
      set FS_DIR = `pwd`
      popd > /dev/null
      set fsdirset = 1;
      breaksw
    case "--masked":
      if ( $#argv < 1) goto arg1err;
      set maskedinputfile = $argv[1]; shift; 
      set maskedinput = 1;
      breaksw
    case "--inputfile":
      if ( $#argv < 1) goto arg1err;
      set inputfilename = $argv[1]; shift; 
      set inputfile = 1;
      breaksw
    case "--segfile": # supplying the volumteric segmentation 10062020
      if ( $#argv < 1) goto arg1err;
      set seginputfile = $argv[1]; shift; 
      set segfileinput = 1;
      breaksw
    case "--forceskullstrip":
      set forceskullstrip = 1;
      breaksw
    case "--cleanup":
      if ( $#argv < 1) goto arg1err;
      set cleanup = $argv[1]; shift;
      breaksw
    case "--newborn":
      set defaultinfants = 0;
      set newborns = 1; # use newborn training set from CNY-BCH for the processing
      set TEMPLATE_SUBJECTS = ($CNYBCH_NEONATES) 
      echo The template subjects will come from: $TEMPLATE_SUBJECTS
      set subjage = 0
      breaksw
    case "--oneyear":
      set defaultinfants = 0;
      set oneyearolds = 1; # use one-year-old training set from CNY-BCH for the processing
      set TEMPLATE_SUBJECTS = ($CNYBCH_AROUNDONE) 
      set subjage = 12
      breaksw
    case "--force":
      set force = 1; 
      breaksw
    case "--avoidtraining":
      if ( $#argv < 1) goto arg1err;
      set avoidtrainingname = $argv[1]; shift;
      set avoidtraining = 1;  # this option is for testing purposes; when set it avoids using the same training subject for the same test subject
      breaksw
    case "--MI":
      set useMI = 1; #  training subject selection with MI
      breaksw
    case "--gmwm":
      set forcegmwm = 1; 
      breaksw
    case "--gmwm2": # This is set to 1 by default now
      set forcegmwm2 = 1; 
      breaksw
    case "--usegpu":
      set usegpu = 1;
      breaksw
    case "--ccseg":
      set computeCCseg = 1;
      breaksw
    case "--usedramms":
      set usedramms = 1;
      set regtype = DRAMMS;
      breaksw
    case "--t2":
      set uset2 = 1;
      breaksw
    case "--t2file":
      if ( $#argv < 1) goto arg1err;
      set t2filename = $argv[1]; shift;
      set uset2 = 1;
      breaksw
    case "--gpuid":
      if ( $#argv < 1) goto arg1err;
      set gpuid = $argv[1]; shift;
      set usegpu = 1;
      breaksw
    case "--stats":
      set dostats = 1; # compute aseg stats
      breaksw
    case "--intnormFSL":
      set intnormFSL = 1; # use alternative intensity normalization
      breaksw
    default:
      echo ERROR: Flag $flag unrecognized.
      echo $cmdline
      exit 1
      breaksw
  endsw
end

##
## check input
##
if($#SUBJECTS_DIR == 0) then
  echo "ERROR: must spec a SUBJECTS_DIR to indicate the location of the input data"
  exit 1;
endif
if($#subj == 0) then
  echo "ERROR: must spec a subject id"
  exit 1;
endif
if (($maskedinput == 0) && !(-e $SUBJECTS_DIR/$subj/mprage.nii.gz)) then
  echo "ERROR: must have the input file in $SUBJECTS_DIR/$subj/mprage.nii.gz!"
  exit 1;
endif
if (($maskedinput == 1) && ($forceskullstrip == 1)) then
  echo "CONFLICT: Both forcing skullstripping and providing skullstripped input."
  exit 1;
endif
#
if (($uset2 == 1) && !($t2filename)) then
  echo "ERROR: must have a T2w file named with --t2file when --t2 flag is used!"
  exit 1;
endif
if (($segfileinput == 1) && ($maskedinput == 0) && !($forceskullstrip)) then
  echo "ERROR: must have a masked mprage file specified with --masked if the segmentation file is provided!"
  exit 1;
endif
# TODO: segmentation must have the FS labels!
if (($segfileinput == 1) && ($maskedinput == 1) && (0)) then # They need to have the same dimensions -- and then conformed
  # TODO: check the size!
  echo "ERROR: The masked mprage and segmentation files have to have the same volume dimensions!"
  exit 1;
endif
if(($defaultinfants) && ($subjage < 0) && ($segfileinput == 0)) then
  echo "ERROR: must set the age (in months!) for the subject to be analyzed"
  exit 1;
endif
if !($defaultinfants + $newborns + $oneyearolds == 1) then
  echo "ERROR: trainingset erroneously defined"
  exit 1;
endif



# If running on the cluster, do not use freeview for checking the results.
if($CLUSTERRUN) then
  set checkresults = 0
endif

#GPU ID
if !($usegpu) then 
  set gpuid = -1
endif

##
## a couple more parameters...
##
set origdir  = $SUBJECTS_DIR/$subj
if !($fsdirset) then 
  set FS_DIR = $origdir
endif

# setenv WORK_DIR $origdir/work
setenv WORK_DIR $FS_DIR/work
if ($force) then 
  rm -fr $WORK_DIR
endif
mkdir -p $WORK_DIR

##
## Setting up the default params and logging utilities 
##
set StartTime = `date  +%s`;
set DateString = "`date '+%y%m%d%H%M'`"
if(! $nolog) then
  set currdir = `pwd`
  set LF  = $WORK_DIR/${subj}_infant_recon_all.$DateString.log
  set sLF = ${LF:h}/summary.${LF:t}
  if(-e $LF) mv $LF $LF.old;
  if(-e $sLF) mv $sLF $sLF.old;
  echo ""
  echo "Log file is $LF"
  echo ""
else
  set LF = /dev/null
endif
echo "Logfile for infant_recon_all" >> $LF
echo "Summary logfile for infant_recon_all" >> $sLF
if ($newborns) then
  echo "** Using CNYBCH newborns as training data set **" >> $LF
  echo "** Using CNYBCH newborns as training data set **" >> $sLF
endif
if ($oneyearolds) then
  echo "** Using CNYBCH one-year-olds as training data set **" >> $LF
  echo "** Using CNYBCH one-year-olds as training data set **" >> $sLF
endif

set tmpMsg = `date`
echo $tmpMsg |& tee -a $LF
echo $tmpMsg >> $sLF
echo $inputargs |& tee -a $LF
echo $inputargs >> $sLF
echo $VERSION |& tee -a $LF
echo $VERSION >> $sLF
set tmpMsg = `hostname`
echo $tmpMsg |& tee -a $LF
echo $tmpMsg >> $sLF
set tmpMsg = `uname -a`
echo $tmpMsg |& tee -a $LF
echo $tmpMsg >> $sLF

########
######## Start the PROCESSING
########

echo Processing the following subject: $subj

## preprocessing the data
echo "Preprocessing step..." >> $sLF

if !($maskedinput) then # the input file is not skullstripped yet

if !($inputfile) then 
  set originput = $origdir/mprage.mgz
  if !(-e $originput) then
    set originput = $origdir/mprage.nii.gz  
  else
    set cmd = (mri_convert $originput ${originput:r}.nii.gz)
    echo $cmd >> $LF
    echo $cmd >> $sLF
    $cmd |& tee -a $LF
    if($status) exit 1; 
  endif
else
  set cmd = (mri_convert $inputfilename $origdir/mprage.nii.gz)   # TODO: check for nifti status and whether this is necessary
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
  if($status) exit 1; 
  set originput = $origdir/mprage.nii.gz
endif

set infile   = $originput
if !(-e $infile) then 
  echo "ERROR: The input data to be processed does not exist:" $infile
  exit 1;
endif
echo "INPUT FILE $infile" | tee -a $sLF | tee -a $LF

# intensity normalization
if !($intnormFSL) then 
  set nufile = $WORK_DIR/${infile:t:r:r}.nu.nii.gz
  if !(-e $nufile) then
    set cmd  = (mri_nu_correct.mni --i $infile --o $nufile --n 2)
    echo $cmd >> $LF
    echo $cmd >> $sLF
    $cmd |& tee -a $LF
    if($status) exit 1; 
  endif
  echo "NU FILE $nufile" | tee -a $sLF | tee -a $LF
  set infile  = $nufile
else
  set maxval = `fslstats $infile -R | awk '{print $2}'`
  # if !(-e $WORK_DIR/${infile:t:r:r}_fslnorm.nii.gz) then
   set cmd = (fslmaths $infile -div $maxval -mul 255 $WORK_DIR/${infile:t:r:r}_fslnorm.nii.gz -odt char)
    echo $cmd >> $LF
    echo $cmd >> $sLF
    $cmd |& tee -a $LF
    if($status) exit 1; 
  #endif
  echo "INTENSITY NORMALIZED FILE $WORK_DIR/${infile:t:r:r}_fslnorm.nii.gz" | tee -a $sLF | tee -a $LF
  set infile  = $WORK_DIR/${infile:t:r:r}_fslnorm.nii.gz
endif
#

##
set outfile = ${infile:r:r}.conf.nii.gz
if !(-e $outfile) then
  set cmd = (mri_convert --conform-dc $infile $outfile)
  echo $cmd >> $LF
  echo $cmd >> $sLF 
  $cmd |& tee -a $LF
  if($status) exit 1;
endif
echo "CONFORMED FILE $outfile" | tee -a $sLF | tee -a $LF

if ($checkresults) then 
  freeview -v $originput $outfile # $nufile 
endif

set preprocessedfile = $outfile

##
## compute masked data
##

set infile     = $preprocessedfile
set maskfile   = ${infile:r:r}.mask.nii.gz
set maskedfile = ${maskfile:r:r}ed.nii.gz

if !(-e $maskfile) then
    # -use-gpu : use GPU versions of the skullstripper: sscnn_skullstrip.py
    # $gpuID = 0 (otherwise user can specify it)
    set cmd = (sscnn_skullstrip -i ${infile} -o $WORK_DIR/sscnn -c t1w --gpu $gpuid) # this will produce the mask file $WORK_DIR/sscnn/sscnn_skullstrip.nii.gz
    echo $cmd >> $LF
    echo $cmd >> $sLF
    $cmd |& tee -a $LF
    if ($status) exit 1;
    set cmd = (mv $WORK_DIR/sscnn/sscnn_skullstrip.nii.gz $maskfile) 
    echo $cmd >> $LF
    echo $cmd >> $sLF
    $cmd |& tee -a $LF
    if ($status) exit 1;
  endif
  echo "MASK FILE $maskfile" | tee -a $sLF | tee -a $LF

  if !(-e $maskedfile) then
    set cmd = (mri_mask $infile $maskfile $maskedfile)
    echo $cmd >> $LF
    echo $cmd >> $sLF
    $cmd |& tee -a $LF
    if ($status) exit 1;
  endif
  echo "MASKED FILE $maskedfile" | tee -a $sLF | tee -a $LF

  if ($checkresults) then 
    freeview -v $infile ${maskedfile}:colormap=heat
  endif

  else
   # set maskedfile = $maskedinputfile # NOTE: this was misplaced -- 10062020
   # set cmd = (mri_convert --conform-dc $maskedinputfile $maskedfile)
   set cmd = (mri_convert --conform-dc $maskedinputfile $WORK_DIR/mprage.nu.conf.masked.nii.gz)
   echo $cmd >> $LF
   echo $cmd >> $sLF
   $cmd |& tee -a $LF
   if ($status) exit 1; 
   set maskedfile = $WORK_DIR/mprage.nu.conf.masked.nii.gz
  endif

echo "MASKED FILE $maskedfile" | tee -a $sLF | tee -a $LF

if ($segfileinput) then
   echo "WORKING WITH EXISTING SEGMENTATION FILE $seginputfile" | tee -a $sLF | tee -a $LF
   #cp $seginputfile $WORK_DIR/finalaseg.nii.gz
   # set cmd = (mri_convert --conform-dc $maskedinputfile $maskedfile)
   set cmd = (mri_convert --conform-dc $seginputfile $WORK_DIR/finalaseg.nii.gz -rt nearest --no_scale 1)
   echo $cmd >> $LF
   echo $cmd >> $sLF
   $cmd |& tee -a $LF
   if ($status) exit 1; 
endif

#if ($checkresults) then 
#  freeview -v $maskedfile ${WORK_DIR/finalaseg.nii.gz}:colormap=lut
#endif

####
## In case TRAINING SUBSET NEEDS TO BE IDENTIFIED
# echo $newborns $oneyearolds $setksize

if !($segfileinput) then # segmentation needs to be computed

if (($newborns || $oneyearolds) && $setksize) then # need to select a subset of the training datasFSSCRIet -- use MI here 
  echo NEED to select a subset of the training dataset....
  set outfname = $WORK_DIR/trainingsubjects_MI.txt
  echo OUTFNAME $outfname
  if ($avoidtraining) then
    set cmd = (find_neighbors_byMI_extended.csh --f $maskedfile -trpool $TEMPLATE_SUBJECTS --k $trainingsetsize --outfile $outfname --checkconflict $avoidtrainingname)
  else
    set cmd = (find_neighbors_byMI_extended.csh --f $maskedfile --trpool $TEMPLATE_SUBJECTS --k $trainingsetsize --outfile $outfname)
  endif
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
  if ($status) exit 1;
  set TEMPLATE_SUBJECTS = (`cat $outfname`)
  echo $TEMPLATE_SUBJECTS
endif
####

##
## compute automated segmentation of the input file
##
set voltype = norm # of the training subjects

##
## SELECT a subset of training subjects if all are used and not newborn or one-year-old
##
#echo $newborns $avoidtraining
if (($newborns) && $avoidtraining) then
  echo Removing traning subject to be avoided $avoidtrainingname....
  set N = $#TEMPLATE_SUBJECTS
  echo Total number of template subjects $N
  set found = 0
  set counter = 1
  foreach s ($TEMPLATE_SUBJECTS)
    if ($s == $avoidtrainingname) then
      set found = 1
      break
    endif
    @ counter = $counter + 1
  end
  if ($found) then
    echo Subject found! $counter
    echo $TEMPLATE_SUBJECTS
    switch ($counter)
      case "1"
        echo $TEMPLATE_SUBJECTS[2-$N]
        set TEMPLATE_SUBJECTS = ($TEMPLATE_SUBJECTS[2-$N])
        #echo $TEMPLATE_SUBJECTS
        breaksw
      case "$N"
        @ endn = $N - 1
        echo ENDN $endn
        set TEMPLATE_SUBJECTS = ($TEMPLATE_SUBJECTS[1-$endn])
        breaksw
      default:
        echo COUNTER $counter
        @ a = $counter - 1
        @ z = $counter + 1
        echo N1 $a
        echo N2 $z
        set TEMPLATE_SUBJECTS = ($TEMPLATE_SUBJECTS[1-$a] $TEMPLATE_SUBJECTS[$z-$N])
        breaksw
    endsw
    echo $N 
    set N = $#TEMPLATE_SUBJECTS
    echo $N 
  endif
  echo $TEMPLATE_SUBJECTS
endif

#echo $defaultinfants
if ($defaultinfants) then 
  echo USING the default set of training dataset....
  set outfname = $WORK_DIR/selectedtrainingsubjects.txt
  echo FINDING $trainingsetsize training subjects to work with...
  set postfix = ()
  if ($avoidtraining) then 
    set postfix = (--checkconflict $avoidtrainingname)
  endif
  set cmd = (find_neighbors_byAge_extended.csh --age $subjage --trages $TEMPLATE_AGES)
  if ($useMI) then 
    set cmd = (find_neighbors_byMI_extended.csh --f $maskedfile)
  endif
  set cmd = ($cmd --trpool $TEMPLATE_SUBJECTS --k $trainingsetsize --outfile $outfname)
  if ($forcegmwm2) then
    set cmd = ($cmd --gmwm --gmwmtrpool $CNYBCH_GMWM_SUBJECTS)
    if !($useMI) then
      set cmd = ($cmd --gmwmtrages $CNYBCH_GMWM_AGES)
    endif
  endif
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
  if ($status) exit 1; 
  set TEMPLATE_SUBJECTS = (`cat $outfname`)
endif

#echo The template subjects will come from: $TEMPLATE_SUBJECTS
if ($forcegmwm) then 
  # Make sure that there is at least one training subject with GMWM
  set found = 0;
  foreach tsubj ($TEMPLATE_SUBJECTS)
    set cmd = (mri_binarize --match 2 41 --count $TEMPLATE_SUBJECTS_DIR/$tsubj/manualseg.count.txt --i $TEMPLATE_SUBJECTS_DIR/$tsubj/manualseg.nii.gz --noverbose)
    echo $cmd >> $LF
    echo $cmd >> $sLF
    $cmd |& tee -a $LF
    if ($status) exit 1;
    set nums = `cat $TEMPLATE_SUBJECTS_DIR/$tsubj/manualseg.count.txt`
    if ($nums[1] > 0) then
      set found = 1; 
      break
    endif
  end
  if !($found) then
    echo "Choose more subjects as training set does not include GMWM!"
    exit 1
  endif
else
  echo The set of training subjects that will be used are: $TEMPLATE_SUBJECTS
endif

# (A) register training subjects and (B) apply transform to the training segmentations: TEMPLATE_SUBJECTS, TEMPLATE_SUBJECTS_DIR
set ref = $maskedfile
foreach tsubj ($TEMPLATE_SUBJECTS)
  if !($subj == $tsubj) then 
    echo TEMPLATE SUBJ $tsubj
    set in           = $TEMPLATE_SUBJECTS_DIR/$tsubj/$voltype.nii.gz
    set templatefile = $TEMPLATE_SUBJECTS_DIR/$tsubj/manualseg.nii.gz
    if !(-e $in) then
      set cmd = (mri_convert ${in:r:r}.mgz $in)
      echo $cmd >> $LF
      echo $cmd >> $sLF
      $cmd |& tee -a $LF
      if ($status) exit 1;
    endif
    if !(-e $templatefile) then
      set cmd = (mri_convert ${templatefile:r:r}.mgz $templatefile)
      echo $cmd >> $LF
      echo $cmd >> $sLF
      $cmd |& tee -a $LF
      if ($status) exit 1;
    endif
    set out         = $WORK_DIR/${tsubj}-2-${subj}.$regtype.nii.gz
    set outtemplate = ${out:h}/manseg-${out:t}
    # set logfile = ${out:r:r}.log
    set def         = ${out:r:r}.field.nii.gz
    if !(-e $def) then 
      if ($regtype == DRAMMS) then
        set cmd = (dramms -S $in -T $ref -O $out -D $def -v -v) # > ${logfile})
        echo $cmd >> $LF
        echo $cmd >> $sLF
        $cmd |& tee -a $LF
        if ($status) exit 1;
      else 
        set cmd = (reg_aladin -ref $ref -flo $in -aff ${def:r:r:r}.affine.txt \
                                           -res ${out:r:r}.affine.nii.gz -voff)
        echo $cmd >> $LF
        echo $cmd >> $sLF
        $cmd |& tee -a $LF
        if ($status) exit 1;
        set cmd = (reg_f3d -ref $ref -flo $in -aff ${def:r:r:r}.affine.txt \
                                              -cpp $def -res $out) # -omp
        echo $cmd >> $LF
        echo $cmd >> $sLF
        $cmd |& tee -a $LF
        if ($status) exit 1;
      endif

      if ($regtype == DRAMMS) then
        set cmd = (dramms-warp $templatefile $def $outtemplate -n)
      else
        set cmd = (reg_resample -ref $ref -flo $templatefile \
                                             -trans $def -res $outtemplate -inter 0)
      endif
      echo $cmd >> $LF
      echo $cmd >> $sLF
      $cmd |& tee -a $LF
      if ($status) exit 1;
      if ($checkresults) then        
        freeview -v $maskedfile $out ${outtemplate}:colormap=lut
      endif
    endif
  endif
end

# (C) labelfusion with kneighbor = 4 (trainingsetsize)
setenv MASKEDINPUT $maskedfile
setenv SEGOUTDIR   $WORK_DIR
set trainingsubjects = ($TEMPLATE_SUBJECTS)
echo TRAINING SUBJECTS LABELFUSION $trainingsubjects | tee -a $sLF | tee -a $LF
# if !(-e $WORK_DIR/${subj}-*labels.mrf.nii.gz) then 
if !(-e $WORK_DIR/aseg.nii.gz) then
  set cmd = (run_label_fusion.csh --subj $subj --regtype $regtype --ts $trainingsubjects)
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
  if ($status) exit 1;  
endif

# TODO: might want to change name in case of multiple files....
set asegfile = $WORK_DIR/${subj}-*labels.mrf.nii.gz
if ($checkresults) then 
  freeview -v $maskedfile ${asegfile}:colormap=lut
endif

#### 12/16/2019: Hack to replace below call (does not work with recent bash call....)
set cmd = (mri_binarize --match 2 41 --count ${asegfile:r:r}.count.txt --i $asegfile --noverbose)
echo $cmd >> $LF
echo $cmd >> $sLF
$cmd |& tee -a $LF
if ($status) exit 1;
set nums = `cat ${asegfile:r:r}.count.txt`
if !($nums[1] > 0) then
# There was no WM label in the training data sets!
  set withGMWM = 0;
endif
####

# (D) postprocessing the segmentation

if !(-e $WORK_DIR/aseg.nii.gz) then 
  # set cmd = (ln -s $asegfile $WORK_DIR/aseg.nii.gz)
  set cmd = (cp $asegfile $WORK_DIR/aseg.nii.gz)
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
  if($status) exit 1;    
endif 
set asegfile = $WORK_DIR/aseg.nii.gz

# switching OLD thalamus labels to NEW FS standard ones
set cmd = (mri_binarize --i $asegfile --replace 48 49 --replace 9 10 --o $asegfile)
echo $cmd >> $LF
echo $cmd >> $sLF
$cmd |& tee -a $LF
if($status) exit 1;    
#

# full brain
if !(-e $WORK_DIR/alllabelmask.nii.gz) then 
  set cmd = (mri_binarize --i $asegfile --o $WORK_DIR/alllabelmask.nii.gz --min 1)
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
  if($status) exit 1; 
  set cmd = (mri_distance_transform $WORK_DIR/alllabelmask.nii.gz 1 3 3 \
                   $WORK_DIR/alllabelmask.distmap.nii.gz)
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
  if($status) exit 1;    
endif

# aseg with GM/WM and not parcellations
set aseggmwmfile = ${asegfile:r:r}.GMWM.nii.gz  # TODO: this might be a misnomer if no GMWM
set cmd = (cp $asegfile $aseggmwmfile)
echo $cmd >> $LF
echo $cmd >> $sLF
$cmd |& tee -a $LF
if($status) exit 1;    

if !(-e ${aseggmwmfile:r:r}.edited.nii.gz) then
  if ($withGMWM) then 
    set cmd = (mri_correct_segmentations $aseggmwmfile ${aseggmwmfile:r:r}.edited.nii.gz)
  else
    set cmd = (mri_correct_segmentations -n $aseggmwmfile ${aseggmwmfile:r:r}.edited.nii.gz)
  endif
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
  if($status) exit 1;  
endif

if !(-e $WORK_DIR/finalaseg.nii.gz) then 
  # set cmd = (ln -s ${aseggmwmfile:r:r}.edited.nii.gz  $WORK_DIR/finalaseg.nii.gz)
  set cmd = (cp ${aseggmwmfile:r:r}.edited.nii.gz  $WORK_DIR/finalaseg.nii.gz)
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
  if($status) exit 1;
endif 

endif # end computing the segmentation


if ($checkresults) then 
  freeview -v $maskedfile $WORK_DIR/finalaseg.nii.gz:colormap=lut
endif 
   
## preprocess for surface creation

set datadir = $WORK_DIR/

# set up FS dir structure
set cmd = (mkdir -p $datadir/$subj/mri/transforms)
echo $cmd >> $LF
echo $cmd >> $sLF
$cmd |& tee -a $LF
if($status) exit 1;
set cmd = (mkdir -p $datadir/$subj/surf)
echo $cmd >> $LF
echo $cmd >> $sLF
$cmd |& tee -a $LF
if($status) exit 1;
set cmd = (mkdir -p $datadir/$subj/label)
echo $cmd >> $LF
echo $cmd >> $sLF
$cmd |& tee -a $LF
if($status) exit 1
set cmd = (mkdir -p $datadir/$subj/stats)
echo $cmd >> $LF
echo $cmd >> $sLF
$cmd |& tee -a $LF
if($status) exit 1;

# mri/norm 
pushd $datadir/$subj/mri

if !(-e norm.nii.gz) then  
  # set cmd = (ln -s $maskedfile norm.nii.gz)
  set cmd = (cp $maskedfile norm.nii.gz)
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
if($status) exit 1;
endif
set cmd = (mri_convert norm.nii.gz norm.mgz)
echo $cmd >> $LF
echo $cmd >> $sLF
$cmd |& tee -a $LF
if($status) exit 1;
if !(-e brain.nii.gz) then  
  set cmd = (ln -s norm.nii.gz brain.nii.gz)
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
  if($status) exit 1;
endif
if !(-e brain.mgz) then  
  set cmd = (ln -s norm.mgz brain.mgz)
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
  if($status) exit 1;
endif
if !(-e brainmask.nii.gz) then  
  set cmd = (ln -s norm.nii.gz brainmask.nii.gz)
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
  if($status) exit 1;
endif
if !(-e brainmask.mgz) then  
  set cmd = (ln -s norm.mgz brainmask.mgz)
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
  if($status) exit 1;
endif
# mri/aseg
if !(-e aseg.nii.gz) then  
  # set cmd = (ln -s $WORK_DIR/finalaseg.nii.gz aseg.nii.gz)
  set cmd = (cp $WORK_DIR/finalaseg.nii.gz aseg.nii.gz)
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
  if($status) exit 1;
endif
set cmd = (mri_convert aseg.nii.gz aseg.mgz)
echo $cmd >> $LF
echo $cmd >> $sLF
$cmd |& tee -a $LF
if($status) exit 1;

popd

unsetenv SUBJECTS_DIR
setenv SUBJECTS_DIR $WORK_DIR

## needed for ribbon / stats files: new registration 06/17/2019
if !(-e $datadir/$subj/mri/transforms/talairach.auto.xfm) then
  pushd $datadir/$subj/mri/
  set targ = $FREESURFER_HOME/average/mni305.cor.mgz
  set cmd = (mri_convert $targ ./${targ:t:r}.nii.gz)
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
  if($status) exit 1;

  set talaffine = $datadir/$subj/mri/transforms/niftyreg_affine.txt
  set cmd = (reg_aladin -ref ./${targ:t:r}.nii.gz -flo $maskedfile -aff $talaffine \
                                     -res ${talaffine:r}.nii.gz  -voff)
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
  if($status) exit 1;

  set cmd = (lta_convert --inniftyreg $talaffine --outlta ${talaffine:r}.lta \
                                     --outmni ${talaffine:r}.xfm --src $maskedfile --trg ./${targ:t:r}.nii.gz)
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
  if($status) exit 1;

  set cmd = (rm -f ./${targ:t:r}.nii.gz)
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
  if($status) exit 1;

  #set cmd = (cp ${talaffine:r}.xfm $datadir/$subj/mri/transforms/talairach.auto.xfm)
  set cmd = (cp ${talaffine:r}.xfm ${talaffine:h}/talairach.auto.xfm)
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
  if($status) exit 1;

  popd
endif

if !(-e $datadir/$subj/mri/transforms/talairach.xfm) then
  set cmd = (cp $datadir/$subj/mri/transforms/talairach.auto.xfm $datadir/$subj/mri/transforms/talairach.xfm)
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
  if($status) exit 1;
endif

##
## add segmentation of the CC 12/17/2019; moved out of talairach loop 12/06/2020
if ($computeCCseg) then
  set cmd = (mri_cc -aseg aseg.mgz -o aseg_CCseg.mgz $subj) #-lta transforms/cc_up.lta
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
if($status) exit 1;
endif
##

##### 12/16/2019: Hack to replace below call (does not work with recent bash call....)
set cmd = (mri_binarize --match 2 41 --count $datadir/$subj/mri/aseg.count.txt --i $datadir/$subj/mri/aseg.nii.gz --noverbose)
echo $cmd >> $LF
echo $cmd >> $sLF
$cmd |& tee -a $LF
if ($status) exit 1;
set nums = `cat $datadir/$subj/mri/aseg.count.txt`
if !($nums[1] > 0) then
  echo "SKIPPING surface creation as no GM/WM separation in the segmentation!"
else
  echo Creating wm surfaces.... >> $LF
endif
#####

if (1) then # 06/14/2017
  set cmd = (create_wm_surfaces_mprage_subject.csh --s $subj) 
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
  if($status) exit 1;
endif

## pial surface
echo Creating pial surfaces.... >> $LF
# if !(-e $SUBJECTS_DIR/$subj/surf/rh.pial) then
if !(-e $SUBJECTS_DIR/$subj/surf/rh.thickness) then
  set cmd = (create_pial_surfaces_mprage_subject.csh --s $subj)
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
  if($status) exit 1;
endif

if ($checkresults) then 
  freeview -v $maskedfile $WORK_DIR/finalaseg.nii.gz:colormap=lut -f $SUBJECTS_DIR/$subj/surf/?h.pial $SUBJECTS_DIR/$subj/surf/?h.white 
endif 

## compute stats
if ($dostats) then
#aparc
  if !(-e $datadir/$subj/mri/aseg.presurf.mgz) then 
    #set cmd = (ln -s $datadir/$subj/mri/aseg.mgz $datadir/$subj/mri/aseg.presurf.mgz)
    set cmd = (cp $datadir/$subj/mri/aseg.mgz $datadir/$subj/mri/aseg.presurf.mgz)
    echo $cmd >> $LF
    echo $cmd >> $sLF
    $cmd |& tee -a $LF
    if($status) exit 1;
  endif
  set cmd = (mris_volmask --label_left_white 2 --label_left_ribbon 3 --label_right_white 41 --label_right_ribbon 42 --save_ribbon --save_distance $subj)
  echo $cmd >> $LF
  echo $cmd >> $sLF
  $cmd |& tee -a $LF
  if($status) exit 1;

  pushd $datadir/$subj/label
  foreach hemi (lh rh)
    set cmd = (mris_anatomical_stats -th3 -mgz -f $datadir/$subj/stats/$hemi.aparc.stats -b -a aparc.annot -c aparc.annot.ctab $subj $hemi)
    echo $cmd >> $LF
    echo $cmd >> $sLF
    $cmd |& tee -a $LF
    if($status) exit 1;
  end
  popd
#aseg
  pushd $datadir/$subj/
  if ($subjage >= 12) then # TODO: Needs further testing (12/06/2020)
    set cmd = (mri_segstats --seg mri/aseg.mgz --sum aseg.stats --pv mri/norm.mgz --empty --brainmask mri/brainmask.mgz --brain-vol-from-seg --excludeid 0 \
                            --excl-ctxgmwm --supratent --subcortgray --in mri/norm.mgz --in-intensity-name norm --in-intensity-units MR --etiv --surf-wm-vol \
                            --surf-ctx-vol --totalgray --euler --ctab $FREESURFER_HOME/ASegStatsLUT.txt --subject $subj)
    echo $cmd >> $LF
    echo $cmd >> $sLF
    $cmd |& tee -a $LF
    if($status) exit 1;
  else # new eTIV computations
    #set template = $INFANTSCRIPTDIR/all_c_robtemplate_affine_mean.05.01.2018.nii.gz
    set template = $FREESURFER_HOME/average/all_c_robtemplate_affine_mean.05.01.2018.nii.gz
    set out  = mri/transforms/${subj}.2.common.nii.gz
    set t_affine = ${out:r:r}.txt
    if !(-e $t_affine) then
      set cmd = (reg_aladin -ref $template -flo mri/norm.nii.gz -aff $t_affine \
                                           -res ${t_affine:r}.nii.gz -voff)
      echo $cmd >> $LF
      echo $cmd >> $sLF
      $cmd |& tee -a $LF
      if($status) exit 1;
      set cmd = (lta_convert --inniftyreg $t_affine --outlta ${t_affine:r}.lta \
                             --outmni ${t_affine:r}.xfm --src mri/norm.nii.gz --trg $template)
      echo $cmd >> $LF
      echo $cmd >> $sLF
      $cmd |& tee -a $LF
      if($status) exit 1;
    endif
    set detfile = mri/transforms/det.txt 
    if !(-e $detfile) then
      set cmd = (lta_diff ${t_affine:r}.lta --dist 5 >> $detfile)
      #echo $cmd >> $LF
      #echo $cmd >> $sLF
      eval $cmd
      #$cmd |& tee -a $LF
      #if($status) exit 1;
    endif
    set num = (400321.078176) # see wiki for explanation
    set det = `tail -n 1 $detfile`
    echo $det \* $num | bc -l
    echo $det \* $num | bc -l >> stats/eTIV.txt

    set cmd = (mri_segstats --seg mri/aseg.mgz --sum aseg.stats --pv mri/norm.mgz --empty --brainmask mri/brainmask.mgz --brain-vol-from-seg --excludeid 0 \
                            --excl-ctxgmwm --supratent --subcortgray --in mri/norm.mgz --in-intensity-name norm --in-intensity-units MR --surf-wm-vol \
                            --surf-ctx-vol --totalgray --euler --ctab $FREESURFER_HOME/ASegStatsLUT.txt --subject $subj)
    echo $cmd >> $LF
    echo $cmd >> $sLF
    $cmd |& tee -a $LF
    if($status) exit 1;
  endif #age
  popd
endif #dostats

##

## to get aparc+aseg
set cmd = (mri_aparc2aseg --s $subj --new-ribbon)
echo $cmd >> $LF
echo $cmd >> $sLF
$cmd |& tee -a $LF
if($status) exit 1;
##
endif

##
echo "Successfully finished infant_recon_all" >> $LF
echo "Successfully finished infant_recon_all" >> $sLF
set EndTime = `date +%s`;
@ DiffTime = $EndTime - $StartTime
set TimeMsg = "infant_recon_all: The whole process took $DiffTime seconds"
echo $TimeMsg |& tee -a $sLF
echo $TimeMsg >> $LF

## copy files to their final location
set cmd = (cp -r $WORK_DIR/$subj/mri $FS_DIR/) 
echo $cmd >> $LF
echo $cmd >> $sLF
$cmd |& tee -a $LF
if($status) exit 1;
set cmd = (cp -r $WORK_DIR/$subj/surf $FS_DIR/)
echo $cmd >> $LF
echo $cmd >> $sLF
$cmd |& tee -a $LF
if($status) exit 1;
set cmd = (cp -r $WORK_DIR/$subj/label $FS_DIR/)
echo $cmd >> $LF
echo $cmd >> $sLF
$cmd |& tee -a $LF
if($status) exit 1;
set cmd = (cp -r $WORK_DIR/$subj/stats $FS_DIR/)
echo $cmd >> $LF
echo $cmd >> $sLF
$cmd |& tee -a $LF
if($status) exit 1;
set cmd = (mkdir -p $FS_DIR/log)
echo $cmd >> $LF
echo $cmd >> $sLF
$cmd |& tee -a $LF
if($status) exit 1;
set cmd = (cp $WORK_DIR/*log $FS_DIR/log/)
echo $cmd >> $LF
echo $cmd >> $sLF
$cmd |& tee -a $LF
if($status) exit 1;

## file cleanup
if ($cleanup) then
  # set filestoremove = ($WORK_DIR/*gz $WORK_DIR/$subj-with* $WORK_DIR/surf $FS_DIR/surf/*tmp $WORK_DIR/work)
  set filestoremove = ($FS_DIR/surf/*tmp)
  set cmd = (rm -f $filestoremove)
  $cmd
  #echo $cmd >> $LF
  #echo $cmd >> $sLF
  #$cmd |& tee -a $LF
  #if($status) exit 1;

  set cmd = (rm -fr $WORK_DIR)
  $cmd
  #echo $cmd >> $LF
  #echo $cmd >> $sLF
  #$cmd |& tee -a $LF
  #if($status) exit 1;

endif


exit 0;

############--------------##################
usage_exit:

if($PrintHelp) then
  cat $0 | \
    awk 'BEGIN{prt=0}{if(prt) print $0; if($1 == "BEGINHELP") prt = 1 }'
else
  echo "Usage: infant_recon_all --s <subj> --age <age_in_months>"
  echo "Help:  infant_recon_all --help"
endif

exit 1;

#---- Everything below is printed out as part of help -----#
#-- During make install, the output of 'fsPrintHelp infant_recon_all.help.xml' -- #
#-- is concatenated to this file --#
BEGINHELP
