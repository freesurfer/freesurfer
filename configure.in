# configure.in
# by y.tosa
# June 17, 2004
#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.
#
AC_PREREQ(2.57)
AC_INIT(Freesurfer, 0.1, tosa@nmr.mgh.harvard.edu)
AC_CONFIG_AUX_DIR([config])
AC_MSG_NOTICE(Setting libtool ...)
AC_PROG_LIBTOOL 
# create specifal dir for intermediate files
AC_MSG_NOTICE(Setting System ...)
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE(Freesurfer, 0.1)
AC_CONFIG_SRCDIR([config.h.in])
AM_CONFIG_HEADER([config.h])

# Checks for programs.
AC_PROG_AWK
AC_PROG_CXX
AC_PROG_CC_STDC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_CPP
AC_PROG_LIBTOOL

################################################################
# define OS
################################################################
# examples are:
# SUSE9.0:   host = x86_64-suse-linux          uname -p: x86_64
# RedHat9.0: host = i686-pc-linux-gnu          uname -p: i686
# MacOSX10 : host = powerpc-apple-darwin6.8    uname -p: powerpc
#################################################################
AC_MSG_NOTICE(Setitng OS from host ...)
case "${host}" in
 i*86-*-linux-gnu*)
 OS=Linux
 OS_CPPFLAGS="-Wall -fwritable-strings"
## choose static version if shared and static present
 OS_LDFLAGS="-all-static"
 DYNAMIC_LIB_EXT=".so"
 SHARED_CFLAGS="-fPIC"
 SHARED_LDFLAGS="-shared"
 ;;
 x86_64-*)
 OS=Linux-x86_64
 OS_CPP_FLAGS="-Wall -fwritable-strings"
 LDFLAGS="-L/usr/lib64 $LDFLAGSA"
 DYNAMIC_LIB_EXT=".so"
 SHARED_CFLAGS="-fPIC"
 SHARED_LDFLAGS="-shared"
 ;; 
 *-apple-darwin*)
 OS=Darwin
 OS_CPPFLAGS=
 OS_LDFLAGS=
 DYNAMIC_LIB_EXT=".dylib"
 SHARED_CFLAGS="-fno-common"
 SHARED_LDFLAGS="-dynamiclib"
 ;;
esac
AC_SUBST(OS)
AC_SUBST(OS_CPPFLAGS)
AC_SUBST(OS_LDFLAGS)
AC_SUBST(DYNAMIC_LIB_EXT)

# export
AC_SUBST(SHARED_CFLAGS)
AC_SUBST(SHARED_LDFLAGS)

##############################################################
# cpu optimization
##############################################################
if test "${CC}" = "gcc"; then 
AC_MSG_NOTICE(Setting CPU optimization ...)
CPUFLAGS=""
CPUTYPE="NO"
AC_MSG_CHECKING(checking for CPU ...)
#### 
#### cpu type: we support 
####     pentium3, pentium4,  
####     athlon-xp, opteron, athlon64, athlon-fx
####      
AC_ARG_WITH(cpu,
  [  --with-cpu=CPUTYPE      where CPUTYPE is pentium3, pentium4, athlon-xp, G4, G5,  ....],
  [CPUTYPE=$withval],[])
# --with-cpu not used  automatically figure out
if test "$CPUTYPE" = "NO"; then
  case "$OS" in
  Darwin)
    maccpu=`machine`
    case "$maccpu" in
      ppc750)
      CPUTYPE=G3
      ;;
      ppc7450)
      CPUTYPE=G4
      ;;
      ppc970)
      CPUTYPE=G5
      ;;
      *)
      CPUTYPE="NO"
      echo "unknown mac cpu $maccpu"
      ;;
      esac
    ;;
  Linux)
    cpu386=`./x86cpucaps/cpucaps -m | sed 's/ //g'`
    case "$cpu386" in
      PentiumIII*)
      CPUTYPE=pentium3
      ;;
      Pentium4)
      CPUTYPE=pentium4
      ;;
      AMDAthlonXP*)
      CPUTYPE=athlon-xp
      ;;
      *) 
      ;;
    esac
    ;;
  Linux64)
    cpu386=`./x86cpucaps/cpucaps -m | sed 's/ //g'`
    case "$cpu386" in
      AMDAthlon64)
      CPUTYPE=athlon64
      ;;
      AMDOpteron*)
      CPUTYPE=opteron
      ;;
      *)
      ;;
    esac
    ;; 
  *)
  ;;
  esac
fi

# now set the flag
case "$CPUTYPE" in 
  pentium3) 
     CPUFLAGS="-march=pentium3 -mfpmath=sse"
     ;;
  pentium4)
     CPUFLAGS="-march=pentium4 -mfpmath=sse -msse2"
     ;;
  athlon-xp)
     CPUFLGAS="-march=athlon-xp -mfpmath=sse"
     ;;	
  opteron)
     CPUFLAGS="-march=opteron -mfpmath=sse -msse2"
     ;;
  athlon64)
     CPUFLAGS="-march=athlon64 -mfpmath=sse -msse2"
     ;;
  athlon-fx)
     CPUFLAGS="-march=athlon-fx -mfpmath=sse"
     ;;
  G4)
     CPUFLAGS="-mtune=G4"
     ;;
  G5)
     CPUFLAGS="-mtune=G5"
     ;;
  *)
     echo "cpu optimization not set"
     ;;
esac

found=$CPUFLAGS 
# set the value

CFLAGS="$CPUFLAGS $CFLAGS"
CXXFLAGS="$CPUFLAGS $CXXFLAGS"

AC_MSG_RESULT($found)
#####
else
  AC_MSG_NOTICE([cpu optimization not done. you must use gcc.])
# if gcc
fi
##############################################################
# mgh put packages and create softlinks
# default path for mni tools
##############################################################
# get the path for X includes and X libraries
AC_PATH_X
ACX_PTHREAD

AC_MSG_NOTICE(Checking directory for MNI, BLT, Tiff and Jpeg)
mghdir=/usr/pubsw/packages
#############################################################
# MNI
#############################################################
ac_mni_includes=NO 
ac_mni_libraries=NO 
ac_mni_bindir=NO
mni_libraries=""
mni_includes=""
AC_MSG_CHECKING(cheking MNI directory)
# default is to use MGH installation
if test -d $mghdir/mni/current ; then
  ac_mni_includes=$mghdir/mni/current/include
  ac_mni_libraries=$mghdir/mni/current/lib
  ac_mni_bin=$mghdir/mni/current/bin
fi
# modify when --with-mni-dir option given
################################################################
AC_ARG_WITH(mni-dir,
  [  --with-mni-dir=DIR      where the root of MNI is installed.],
  [  ac_mni_includes="$withval"/include
     ac_mni_libraries="$withval"/lib
     ac_mni_bindir="$withval"/bin

  ])

# modify CXXFLAGS and LDFLAGS
if test ! "$ac_mni_includes" = "NO"; then
  AC_MSG_RESULT(MNI directory is $withval)	
  mni_includes=-I$ac_mni_includes
  CPPFLAGS="$CPPFLAGS $mni_includes"
else
  AC_MSG_RESULT(MNI directory is not supplied)
fi
if test ! "$ac_mni_libraries" = "NO"; then 
  mni_libraries=-L$ac_mni_libraries 
  LDFLAGS="$LDFLAGS $mni_libraries"
fi

AC_SUBST(mni_includes)
AC_SUBST(mni_libraries)


################################################################
# BLT
################################################################
ac_blt_includes="NO"
ac_blt_libraries="NO"
ac_blt_bindir="NO"
blt_includes=""
blt_libraries=""
AC_MSG_CHECKING(cheking BLT directory)
# default is to use MGH installation
# MGH installs BLT in tcl directory
if test -d $mghdir/tcl/current ; then
  ac_blt_includes=$mghdir/tcl/current/include
  ac_blt_libraries=$mghdir/tcl/current/lib
  ac_blt_bindir=$mghdir/tcl/current/bin
fi

AC_ARG_WITH(BLT-dir,
  [  --with-BLT-dir=DIR      where the root of Blt/Tk is installed.],
  [  ac_blt_includes="$withval"/include
     ac_blt_libraries="$withval"/lib
     ac_blt_bindir="$withval"/bin
  ])

# modify CXXFLAGS and LDFLAGS
if test ! "$ac_blt_includes" = "NO"; then
  AC_MSG_RESULT(BLT directory is $withval)
  blt_includes=-I$ac_blt_includes
  CPPFLAGS="$CPPFLAGS $blt_includes"
else
  AC_MSG_RESULT(BLT dirctory is not supplied)
fi
if test ! "$ac_blt_libraries" = "NO"; then
  blt_libraries=-L$ac_blt_libraries 
fi

AC_SUBST(blt_includes)
AC_SUBST(blt_libraries)

################################################################
# tiff   Mac does not have this
################################################################
ac_tiff_includes="NO"
ac_tiff_libraries="NO"
ac_tiff_bindir="NO"
tiff_includes=""
tiff_libraries=""
AC_MSG_CHECKING(cheking Tiff directory)
# default is to use MGH installation
if test -d $mghdir/tiff/current ; then
  ac_tiff_includes=$mghdir/tiff/current/include
  ac_tiff_libraries=$mghdir/tiff/current/lib
  ac_tiff_bindir=$mghdir/tiff/current/bin
fi

AC_ARG_WITH(tiff-dir,
  [  --with-tiff-dir=DIR     where the root of Tiff is installed.],
  [  ac_tiff_includes="$withval"/include
     ac_tiff_libraries="$withval"/lib
     ac_tiff_bindir="$withval"/bin
  ])

# modify CXXFLAGS and LDFLAGS
if test ! "$ac_tiff_includes" = "NO"; then
  AC_MSG_RESULT(Tiff directory is $withval)
  tiff_includes=-I$ac_tiff_includes
  CPPFLAGS="$CPPFLAGS $tiff_includes"
else
  AC_MSG_RESULT(Tiff directory is not supplied)
fi
if test ! "$ac_tiff_libraries" = "NO"; then 
  tiff_libraries=-L$ac_tiff_libraries 
  LDFLAGS="$LDFLAGS $tiff_libraries"
fi

################################################################
# jpeg   Mac does not have this
################################################################
ac_jpeg_includes="NO"
ac_jpeg_libraries="NO"
ac_jpeg_bindir="NO"
jpeg_includes=""
jpeg_libraries=""
AC_MSG_CHECKING(cheking Jpeg directory)
# default is to use MGH installation
if test -d $mghdir/jpeg/current ; then
  ac_jpeg_includes=$mghdir/jpeg/current/include
  ac_jpeg_libraries=$mghdir/jpeg/current/lib
  ac_jpeg_bindir=$mghdir/jpeg/current/bin
fi

AC_ARG_WITH(jpeg-dir,
  [  --with-jpeg-dir=DIR     where the root of Jpeg is installed.],
  [  ac_jpeg_includes="$withval"/include
     ac_jpeg_libraries="$withval"/lib
     ac_jpeg_bindir="$withval"/bin
  ])

# modify CXXFLAGS and LDFLAGS
if test ! "$ac_jpeg_includes" = "NO"; then
  AC_MSG_RESULT(Jpeg directory is $withval)
  jpeg_includes=-I$ac_jpeg_includes
  CPPFLAGS="$CPPFLAGS $jpeg_includes"
else
  AC_MSG_RESULT(Jpeg directory is not supplied)
fi
if test ! "$ac_jpeg_libraries" = "NO"; then 
  jpeg_libraries=-L$ac_jpeg_libraries 
  LDFLAGS="$LDFLAGS $jpeg_libraries"
fi

# generic mods
CPPFLAGS="$CPPFLAGS -D$OS -DANSI $OS_CPPFLAGS"

AC_MSG_NOTICE(Get OpenGL info ...)
#####################################################
# OpenGL
#####################################################
AX_CHECK_GLUT
#####################################################
# produces GLUT_CFLAGS GLUT_LIBS
LIBS_OPENGL="$GLUT_LIBS"

#####################################################
# Tcl/Tk
####################################################
AC_MSG_NOTICE(checking Tcl/Tk directory)
################# apple
AC_ARG_WITH([apple-tcl-framework],
	    [AC_HELP_STRING([--with-apple-tcl-framework])])
if test "X$with_apple_tcl_framework" = "Xyes"; then
  AC_MSG_ERROR("FATAL:tkmedit/tksurfer needs BLT.  No Aqua verson of BLT exists yet.")
#  AC_MSG_NOTICE([Apple framework way ...])
#  AC_DEFINE([HAVE_APPLE_TCL_FRAMEWORK],[1],
#            [Use the Apple Tcl framework,])
#  TCL_CFLAGS="-framework Tcl -framework Tk"
#  TCL_LIBS="-lTix8.4"
#  tcl_libraries="-L/Library/Tcl/Tix8.4"
#  tcl_includes=""
else
################## other cases
# here are uni*es
  AC_MSG_NOTICE([unix style...])
  ac_tcl_includes="NO"
  ac_tcl_libraries="NO"
  ac_tcl_bindir="NO"
  tcl_includes=""
  tcl_libraries=""

# check for MGH style
# default is to use MGH installation
  if test -d $mghdir/tcl/current ; then
    ac_tcl_includes=$mghdir/tcl/current/include
    ac_tcl_libraries=$mghdir/tcl/current/lib
    ac_tcl_bindir=$mghdir/tcl/current/bin
  
  fi

  AC_ARG_WITH(tcl-dir,
    [  --with-tcl-dir=DIR      where the root of Tcl/Tk is installed.],
    [  ac_tcl_includes="$withval"/include
       ac_tcl_libraries="$withval"/lib
       ac_tcl_bindir="$withval"/bin
    ])

# modify CXXFLAGS and LDFLAGS
  if test ! "$ac_tcl_includes" = "NO"; then
    AC_MSG_RESULT([Tcl directory is $withval])
    tcl_includes=-I$ac_tcl_includes
    CPPFLAGS="$CPPFLAGS $tcl_includes"
  else
    AC_MSG_RESULT([Tcl directory is not supplied])
  fi

  if test ! "$ac_tcl_libraries" = "NO"; then 
    tcl_libraries=-L$ac_tcl_libraries 
  fi

  TCL_CFLAGS="$blt_includes $tcl_includes"
fi
###### end of uni*es
AC_SUBST(tcl_includes)
AC_SUBST(tcl_libraries)

################################################
LIBDIRS_MGH="utils rgb unix dicom hipsstubs"
AC_SUBST(LIBDIRS_MGH)

# stupid circular dependency on iopen swapInt etc.
LIBS_MGH="utils/libutils.a rgb/librgb.a unix/libunix.a dicom/libdicom.a hipsstubs/libhipsstubs.a"
AC_SUBST(LIBS_MGH)

#####################################################
# Checks for libraries.
# the order is important
#####################################################
AC_MSG_NOTICE(Making sure that all libraries can be compilable ...)
AC_CHECK_LIB([m], [floor])
AC_CHECK_LIB([z], [gzopen])
AC_CHECK_LIB([crypt], [crypt],[],[echo "MacOSX won't need this"])
AC_CHECK_LIB([tiff], [main])
AC_CHECK_LIB([jpeg], [jpeg_start_compress])
# three mni libs
AC_CHECK_LIB([netcdf], [nccreate],[],
  [AC_MSG_ERROR("FATAL:netcdf lib not found.  Set LDFLAGS or --with-mni-dir.")] )
AC_CHECK_LIB([minc], [main], [],
  [AC_MSG_ERROR("FATAL:minc lib not found.  Set LDFLAGS or --with-mni-dir.")] )
AC_CHECK_LIB([volume_io], [transform_point],[],
  [AC_MSG_ERROR("FATAL:volume_io lib not found.  Set LDFLAGS or --with-mni-dir.")] )
# the entire libs here is passed as LIBS
# thus save the current one
ac_save_libs=$LIBS
ac_save_cppflags=$CPPFLAGS
ac_save_ldflags=$LDFLAGS

## check Tcl/Tk/BLT
CPPFLAGS="$CPPFLAGS $TCL_CFLAGS"
LDFLAGS="$LDFLAGS $tcl_libraries"	
################## Tcl
AC_CHECK_LIB([tcl],[Tcl_Init],
  [LIB_TCL=-ltcl],
# check tcl8.4
  [
    AC_CHECK_LIB([tcl8.4],[Tcl_Init],
    [LIB_TCL=-ltcl8.4],
# check tcl8.3
    [ AC_CHECK_LIB([tcl8.3],[Tcl_Init],
      [LIB_TCL=-ltcl8.3],
      [AC_MSG_ERROR("FATAL: libtcl not found. check config.log. set LDFLAGS or --with-tcl-dir.")        ] )
    ])	
  ])
#################### Tk  needs X11 (or Aqua)
LIBS="-lX11 $LIBS"
CPPFLAGS="$GLUT_CFLAGS $CPPFLAGS"
LDFLAGS="-L$x_libraries $LDFLAGS"
AC_CHECK_LIB([tk],[Tk_Init],
  [LIB_TK=-ltk],
# check tk8.4
  [
    AC_CHECK_LIB([tk8.4],[Tk_Init],
    [LIB_TK=-ltk8.4],
# check tk8.3
    [ 
      AC_CHECK_LIB([tk8.3],[Tk_Init],
      [LIB_TK=-ltk8.3],
      [AC_MSG_ERROR("FATAL: libtk not found. check config.log. set LDFLAGS or --with-tcl-dir.")
      ])
    ])  
  ])

##################### Tix 
AC_CHECK_LIB([tix],[Tix_Init],
  [LIB_TIX=-ltix],
  [
# check tix8.1.8.4
    AC_CHECK_LIB([tix8.1.8.4], [Tix_Init],
    [LIB_TIX=-ltix8.1.8.4],
    [
# check tix8.1.8.3
      AC_CHECK_LIB([tix.8.1.8.3], [Tix_Init],
      [LIB_TIX=-ltix8.1.8.3],
      [AC_MSG_ERROR("FATAL: libtix not found. check config.log. set LDFLAGS or --with-tcl-dir.")
      ])
    ])
  ])
# if test "X$with_apple_tcl_framework" = "Xyes"; then
#   AC_CHECK_LIB([Tix8.4],[Tix_Init],[],
#              [AC_MSG_ERROR("FATAL: libTix8.4 not found.")])

##################### BLT (we cannot build BLT in Aqua)   must use X11
LDFLAGS="$LDFLAGS $blt_libraries"
LIBS="$LIB_TK $LIB_TCL $LIBS"
AC_CHECK_LIB([BLT],[Blt_Init],
  [LIB_BLT=-lBLT],
# check BLT.2 (MacOSX)
  [
    AC_CHECK_LIB([BLT.2],[BLT_Init],
    [LIB_BLT=-lBLT.2],
    [AC_MSG_ERROR("FATAL: libBLT not found. check config.log.  Set LDFLAGS or --with-BLT-dir.")
    ])
  ])

# recover previous one without X and Tcl
LIBS=$ac_save_libs
CPPFLAGS=$ac_save_cppflags
LDFLAGS=$ac_save_ldflags

LIBS_TCL="$LIB_BLT $LIB_TIX $LIB_TK $LIB_TCL"
LIBS_TCL_OPENGL="$LIBS_TCL $LIBS_OPENGL"

AC_SUBST(LIBS_TCL)
AC_SUBST(LIBS_TCL_OPENGL)
AC_SUBST(TCL_CFLAGS)

###########################################################################################
# Checks for header files.
############################################################################################
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS([arpa/inet.h fcntl.h float.h limits.h locale.h malloc.h memory.h netdb.h netinet/in.h stddef.h stdlib.h string.h strings.h sys/file.h sys/ioctl.h sys/param.h sys/socket.h sys/time.h sys/timeb.h unistd.h values.h wctype.h])
# the following is needed on darwin
AC_CHECK_HEADERS(sys/types.h stdint.h)

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_MODE_T
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM
AC_CHECK_TYPES([ptrdiff_t])

# Checks for library functions.
AC_FUNC_CLOSEDIR_VOID
AC_FUNC_ERROR_AT_LINE
AC_FUNC_LSTAT
AC_FUNC_LSTAT_FOLLOWS_SLASHED_SYMLINK
AC_FUNC_MALLOC
AC_FUNC_MMAP
AC_FUNC_SELECT_ARGTYPES
AC_TYPE_SIGNAL
AC_FUNC_STAT
AC_FUNC_STRTOD
AC_FUNC_VPRINTF
# freesurfer uses calloc, getenv often so added
AC_CHECK_FUNCS([bzero calloc floor ftime getenv getcwd getpagesize gettimeofday memmove memset mkdir munmap pow putenv rint select setenv sqrt strcasecmp strchr strdup strerror strncasecmp strrchr strspn strstr strtol uname])

###############################################################################
# output
###############################################################################
./config/removemakefile.sh

AC_OUTPUT(\
Makefile \
unix/Makefile dicom/Makefile rgb/Makefile utils/Makefile hipsstubs/Makefile \
mris2rgb/Makefile nmovie/Makefile mris_show/Makefile tkmedit/Makefile tkregister/Makefile \
tkregister2/Makefile tksurfer/Makefile \
check_siemens_dir/Makefile repair_siemens_file/Makefile mkxsubjreg/Makefile \
rbftest/Makefile rawtocor/Makefile mri_add_xform_to_header/Makefile mri_auto_fill/Makefile \
mri_average/Makefile mri_build_priors/Makefile mri_ca_label/Makefile mri_ca_normalize/Makefile \
mri_ca_tissue_parms/Makefile mri_ca_register/Makefile mri_ca_train/Makefile \
mri_cnr/Makefile mri_compute_overlap/Makefile mri_convert/Makefile mri_convert_mdh/Makefile \
mri_copy_values/Makefile mri_deface/Makefile mri_edit_segmentation/Makefile \
mri_edit_segmentation_with_surfaces/Makefile mri_em_register/Makefile mri_estimate_tissue_parms/Makefile \
mri_evaluate_morph/Makefile mri_extract/Makefile mri_extract_conditions/Makefile \
mri_extract_label/Makefile mri_fill/Makefile mri_gca_ambiguous/Makefile mri_gdfglm/Makefile \
mri_head/Makefile mri_histo_eq/Makefile mri_info/Makefile mri_joint_density/Makefile \
mri_label_histo/Makefile mri_label_vals/Makefile mri_label_volume/Makefile mri_label2label/Makefile \
mri_linear_register/Makefile mri_make_bem_surfaces/Makefile mri_make_template/Makefile \
mri_mark_temporal_lobe/Makefile mri_mask/Makefile mri_ms_fitparms/Makefile \
mri_nlfilter/Makefile mri_normalize/Makefile mri_partial_ribbon/Makefile mri_paint/Makefile \
mri_parselabel/Makefile mri_parse_sdcmdir/Makefile mri_polv/Makefile mri_probedicom/Makefile mri_probe_ima/Makefile \
mri_reduce/Makefile mri_register/Makefile mri_relabel_hypointensities/Makefile \
mri_remove_neck/Makefile mri_ribbon/Makefile mri_rigid_register/Makefile \
mri_segment/Makefile mri_surfacemask/Makefile mri_strip_skull/Makefile \
mri_strip_subject_info/Makefile mri_strip_nonwhite/Makefile mri_surf2surf/Makefile \
mri_surf2vol/Makefile mri_synthesize/Makefile mri_tessellate/Makefile mri_transform/Makefile \
mri_train/Makefile mri_twoclass/Makefile mri_volcluster/Makefile mri_volsynth/Makefile \
mri_vol2roi/Makefile mri_vol2surf/Makefile mri_watershed/Makefile mri_wmfilter/Makefile \
mri_xvolavg/Makefile stim/Makefile stim_polar/Makefile tridec/Makefile \
mris_add_template/Makefile mris_anatomical_stats/Makefile mris_average_curvature/Makefile \
mris_ca_label/Makefile mris_classify_thickness/Makefile mris_compute_acorr/Makefile \
mris_compute_overlap/Makefile mris_convert/Makefile mris_curvature/Makefile \
mris_curvature_stats/Makefile mris_entropy/Makefile mris_errors/Makefile \
mris_euler_number/Makefile mris_expand/Makefile mris_fix_topology/Makefile \
mris_flatten/Makefile mris_glm/Makefile mris_inflate/Makefile mris_jacobian/Makefile \
mris_label_area/Makefile mris_label_mode/Makefile mris_make_average_surface/Makefile \
mris_make_surfaces/Makefile mris_make_template/Makefile \
mris_morph_stats/Makefile mris_multiscale_stats/Makefile \
mris_remove_variance/Makefile mris_rescale/Makefile mris_reverse/Makefile mris_rotate/Makefile \
mris_ms_refine/Makefile mris_register/Makefile mris_sample_label/Makefile \
mris_sample_parc/Makefile mris_segment_vals/Makefile mris_smooth/Makefile mris_sphere/Makefile \
mris_spherical_average/Makefile mris_surface_to_vol_distances/Makefile mrisp_paint/Makefile \
mris_talairach/Makefile mris_thickness/Makefile mris_thickness_comparison/Makefile \
mris_transform/Makefile mris_translate_annotation/Makefile mris_twoclass/Makefile \
mris_w_to_curv/Makefile fsgdf/Makefile) 


