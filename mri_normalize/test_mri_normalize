#!/bin/tcsh -f
#
# regression test for: mri_normalize
#


umask 002

# extract testing data:
gunzip -c testdata.tar.gz | tar xvf -


#
# run mri_normalize using typical input file formats:
#

cd testdata

setenv FREESURFER_HOME ../../distribution
setenv SUBJECTS_DIR ""


# command 1: -mprage
set cmd=(../mri_normalize -mprage nu.mgz T1.mgz)
echo
$cmd
if ($status != 0) then
  echo "mri_normalize FAILED"
  exit 1
endif

# command 2: -aseg
set cmd=(../mri_normalize -aseg aseg.presurf.mgz norm.mgz brain.mgz)
echo
$cmd
if ($status != 0) then
  echo "mri_normalize FAILED"
  exit 1
endif

# command 1: -gentle
set cmd=(../mri_normalize -gentle nu.mgz T1_gentle.mgz)
echo
$cmd
if ($status != 0) then
  echo "mri_normalize FAILED"
  exit 1
endif

echo
echo
echo


#
# compare expected results with produced results:
#

set TEST_FILES=( T1.mgz brain.mgz T1_gentle.mgz)
foreach tstvol ($TEST_FILES)
  # now compare expected with observed data
  set REF_VOL = freesurfer.mgz

  if ( "x$tstvol" == "xT1.mgz" ) then
    set REF_VOL = ref_T1.mgz
  endif

  if ( "x$tstvol" == "xbrain.mgz" ) then
    set REF_VOL = ref_brain.mgz
  endif

  if ( "x$tstvol" == "xT1_gentle.mgz" ) then
    set REF_VOL = ref_T1_gentle.mgz
  endif

  set TST_VOL  = $tstvol
  set DIFF_VOL = $PWD/mri_diff-$tstvol
  set MRIDIFFF = $PWD/mri_diff-$tstvol.txt

  set cmd=(../../mri_diff/mri_diff --debug \
            --thresh 0.0 \
            --res-thresh 0.000001 \
            --geo-thresh 0.000008 \
            --log $MRIDIFFF \
            $REF_VOL $TST_VOL --diff $DIFF_VOL);
  echo
  echo $cmd
  $cmd
  set diff_status=$status
  if ($diff_status != 0) then
    echo "$cmd FAILED (exit status=$diff_status)"
    exit 1
  endif
end


# cleanup:
cd ..
rm -rf testdata

echo
echo "test_mri_normalize passed test"
exit 0