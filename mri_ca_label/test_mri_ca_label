#!/bin/tcsh -f

#
# test_mri_ca_label
#
# execute mri_ca_label and check results against expected results
#
# Original Author: Nick Schmansky
# CVS Revision Info:
#    $Author: zkaufman $
#    $Date: 2015/01/08 22:12:37 $
#    $Revision: 1.3 $
#
# Copyright Â© 2014 The General Hospital Corporation (Boston, MA) "MGH"
#
# Terms and conditions for use, reproduction, distribution and contribution
# are found in the 'FreeSurfer Software License Agreement' contained
# in the file 'LICENSE' found in the FreeSurfer distribution, and here:
#
# https://surfer.nmr.mgh.harvard.edu/fswiki/FreeSurferSoftwareLicense
#
# Reporting: freesurfer@nmr.mgh.harvard.edu
#
# General inquiries: freesurfer@nmr.mgh.harvard.edu
#

set VERSION='$Id: test_mri_ca_label,v 1.3 2015/01/08 22:12:37 zkaufman Exp $'

umask 002

#
# extract testing data
#
gunzip -c testdata.tar.gz | tar xvf -

#
# run mri_ca_label using typical input file formats,
# first with one thread then with 8 threads.
#

cd testdata

set log = (../test_mri_ca_label.log)
set threads=( 1 8 )
if ($?SKIP_MRI_CALABEL_1CPU_TEST) set threads=( 8 )
#goto compare

if (-e $log) rm -f $log 

foreach num ($threads)

    setenv OMP_NUM_THREADS $num
    set cmd=(../mri_ca_label \
        -relabel_unlikely 9 .3 \
        -prior 0.5 \
        -align \
        norm.mgz \
        talairach.m3z \
        ../../distribution/average/RB_all_2014-08-21.gca \
        aseg.auto_noCCseg.${num}cpu.mgz)
    echo "\n\n $cmd \n\n" |& tee -a $log
    $cmd |& tee -a $log
    if ($status != 0) then
        echo "mri_ca_label FAILED" |& tee -a $log
    exit 1
    endif

end

echo "" |& tee -a $log
echo "" |& tee -a $log
echo "" |& tee -a $log

#
# compare expected results with actual (produced) results
#
compare:
if ("`uname -s`" == "Darwin") cp aseg.auto_noCCseg.macos.mgz aseg.auto_noCCseg.mgz

foreach num ($threads)

  set cmd=(../../mri_diff/mri_diff \
    aseg.auto_noCCseg.mgz aseg.auto_noCCseg.${num}cpu.mgz);
  echo "\n\n $cmd \n\n" |& tee -a $log
  $cmd |& tee -a $log
  if ($status != 0) then
    echo "$cmd FAILED" |& tee -a $log
    exit 1
  endif

end

#
# cleanup
#
cd ..
rm -Rf testdata

echo "" |& tee -a $log
echo "test_mri_ca_label passed all tests" |& tee -a $log
exit 0
