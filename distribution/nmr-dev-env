#############################################################################
# Name:    nmr-dev-env
# Purpose: sets up the environment to run the MGH-NMR standard MRI processing
#          stream: unpacking, functional, structural, and visualization.
# Usage:   source nmr-dev-env
#
# $Id: nmr-dev-env,v 1.17 2007/08/13 22:17:33 nicks Exp $
#
# NOTE: this file is under version control. Make sure to use the
# proper check-out/check-in procedures when modifying it!
#############################################################################

# check if attempting to execute on a machine which should not run
# Freesurfer (such as 'gate' and 'entry')
if( -e /etc/NOFREESURFER ) then
  cat /etc/NOFREESURFER
  exit
endif

# track usage
set USER_LIST_PATH=/autofs/space/freesurfer
echo "`date`|`whoami`|`hostname -s`|nmr-dev-env" >> $USER_LIST_PATH/nmr-dev-env_user_list.txt

set VERSION = '$Id: nmr-dev-env,v 1.17 2007/08/13 22:17:33 nicks Exp $';
set NSE_VERSION = '${VERSION}';
if( $?FS_FREESURFERENV_NO_OUTPUT ) then
  echo $NSE_VERSION
endif

## Turn on verboseness if desired ##
if($?NMR_DEV_VERBOSE) then
  set echo
  set verbose
endif

setenv FREESURFER_HOME /usr/local/freesurfer/dev
unsetenv NO_FSFAST
setenv FSFAST_HOME     $FREESURFER_HOME/fsfast
unsetenv NO_MINC

# Source the FreeSurfer Environment File
set FS_ENV_FILE =  $FREESURFER_HOME/SetUpFreeSurfer.csh
if(! -e $FS_ENV_FILE) then
  echo "ERROR: cannot find $FS_ENV_FILE"
  exit 1;
endif
setenv SET_TCL_VARS 1
source $FS_ENV_FILE

# TCLLIBPATH is defined in FreeSurferEnv.csh to ensure compatibility
# in the public distributions, but creates a tix library
# conflict with FSL, so unset it.
unsetenv TCLLIBPATH

# Source the FreeDiffusion Environment File
set FD_ENV_FILE =  $FREESURFER_HOME/SetUpFreeDiffusion.csh
if(! -e $FD_ENV_FILE) then
  echo "WARNING: cannot find $FD_ENV_FILE"
else
  source $FD_ENV_FILE
endif

### Create a prompt to indicate the standard environment ###
set this_shell    = `basename $SHELL`;
set this_hostname = `hostname` 
set this_host     = `basename $this_hostname .nmr.mgh.harvard.edu`
if(! $?FSENV_KEEP_PROMPT ) then
  if($this_shell == csh) then
    unalias cd
    alias cd  'set old=$cwd; chdir \!*; 
    set tmpcwd = `basename $cwd`; 
    set prompt = "$this_host"":$tmpcwd (nmr-dev-env) % "'
    cd $cwd;
  else
    set prompt = "[%m:%c] (nmr-dev-env) "
  endif
endif

### Dont leave huge cores around ###
limit coredumpsize 0

# Add pubsw and AFNI to the path if it isn't there.
if ( "${path}" !~ */usr/pubsw/bin* ) then
  set path = ( /usr/pubsw/bin $path )
endif

if ( "${path}" !~ */usr/pubsw/packages/AFNI/current/bin* ) then
  set path = ( /usr/pubsw/packages/AFNI/current/bin $path )
endif

if(! $?MRI_UMASK) then
  setenv MRI_UMASK 2
endif

rehash;

unset echo ;
unset verbose;

exit 0;
####################################################################
