#############################################################################
# Name:    nmr-dev-env
# Purpose: sets up the environment to run the MGH-NMR standard MRI processing
#          stream: unpacking, functional, structural, and visualization.
# Usage:   source nmr-dev-env
#
# $Id: nmr-dev-env,v 1.11 2005/09/18 06:34:04 nicks Exp $
#
# NOTE: this file is under version control. Make sure to use the
# proper check-out/check-in procedures when modifying it!
#############################################################################

if ("`uname -s`" == "Darwin") then 
  set USER_LIST_PATH=/autofs/space/freesurfer
else
  set USER_LIST_PATH=/space/freesurfer
endif
echo "`date`|`whoami`|`hostname -s`|nmr-dev-env" >> $USER_LIST_PATH/nmr-dev-env_user_list.txt

set NSE_VERSION = '$Id: nmr-dev-env,v 1.11 2005/09/18 06:34:04 nicks Exp $';
if( $?FS_FREESURFERENV_NO_OUTPUT ) then
  echo $NSE_VERSION
endif

## Turn on verboseness if desired ##
if($?NMR_DEV_VERBOSE) then
  set echo
  set verbose
endif

set VERSION = '$Id: nmr-dev-env,v 1.11 2005/09/18 06:34:04 nicks Exp $';

setenv FREESURFER_HOME /usr/local/freesurfer/dev
if(! $?SUBJECTS_DIR) then
  if (-e /space/sake/3/users/inverse/subjects) setenv SUBJECTS_DIR /space/sake/3/users/inverse/subjects
  if (-e /autofs/space/sake_003/users/inverse/subjects) setenv SUBJECTS_DIR /autofs/space/sake_003/users/inverse/subjects
endif
unsetenv NO_FSFAST
setenv FSFAST_HOME     $FREESURFER_HOME/fsfast
unsetenv NO_MINC

# Source the FreeSurfer Environment File
set FS_ENV_FILE =  $FREESURFER_HOME/SetUpFreeSurfer.csh
if(! -e $FS_ENV_FILE) then
  echo "ERROR: cannot find $FS_ENV_FILE"
  exit 1;
endif
source $FS_ENV_FILE

# Necessary for FSL to work w/ some freesurfer tools:
setenv FSLOUTPUTTYPE ANALYZE

# Source the FreeDiffusion Environment File
set FD_ENV_FILE =  $FREESURFER_HOME/SetUpFreeDiffusion.csh
if(! -e $FD_ENV_FILE) then
  echo "WARNING: cannot find $FD_ENV_FILE"
else
  source $FD_ENV_FILE
endif

### Create a prompt to indicate the standard environment ###
set this_shell    = `basename $SHELL`;
set this_hostname = `hostname` 
set this_host     = `basename $this_hostname .nmr.mgh.harvard.edu`
if(! $?FSENV_KEEP_PROMPT ) then
  if($this_shell == csh) then
    unalias cd
    alias cd  'set old=$cwd; chdir \!*; 
    set tmpcwd = `basename $cwd`; 
    set prompt = "$this_host"":$tmpcwd (nmr-dev-env) % "'
    cd $cwd;
  else
    set prompt = "[%m:%c] (nmr-dev-env) "
  endif
endif

### Dont leave huge cores around ###
limit coredumpsize 0

# Add pubsw and AFNI to the path if it isn't there.
if ( "${path}" !~ */usr/pubsw/bin* ) then
  set path = ( /usr/pubsw/bin $path )
endif

if ( "${path}" !~ */usr/pubsw/packages/AFNI/current/bin* ) then
  set path = ( /usr/pubsw/packages/AFNI/current/bin $path )
endif

if(! $?MRI_UMASK) then
  setenv MRI_UMASK 2
endif

rehash;

unset echo ;
unset verbose;

exit 0;
####################################################################
