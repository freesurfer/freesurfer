#############################################################################
# Name:    nmr-dev-env
# Purpose: sets up the environment to run the MGH-NMR standard MRI processing
#          stream: unpacking, functional, structural, and visualization.
# Usage:   source nmr-dev-env
#
# $Id: nmr-dev-env,v 1.1 2005/03/01 23:05:14 kteich Exp $
#
# NOTE: this file is under version control. Make sure to use the
# proper check-out/check-in procedures when modifying it!
#############################################################################

set NSE_VERSION = '$Id: nmr-dev-env,v 1.1 2005/03/01 23:05:14 kteich Exp $';
echo $NSE_VERSION

## Turn on verboseness if desired ##
if($?NMR_DEV_VERBOSE) then
  set echo
  set verbose
endif

set VERSION = '$Id: nmr-dev-env,v 1.1 2005/03/01 23:05:14 kteich Exp $';



setenv FREESURFER_HOME /usr/local/freesurfer/dev
if(! $?SUBJECTS_DIR) then
	setenv SUBJECTS_DIR    /space/sake/3/users/inverse/subjects
endif
unsetenv NO_FSFAST
setenv FSFAST_HOME     $FREESURFER_HOME/fsfast
unsetenv NO_MINC

setenv MINC_BIN_DIR /usr/pubsw/packages/mni/current/bin
setenv MINC_LIB_DIR /usr/pubsw/packages/mni/current/lib

# Set up FSL.
setenv FSLDIR  /usr/pubsw/packages/fsl/current
setenv FSL_DIR  $FSLDIR
setenv FSL_BIN  $FSLDIR/bin
source $FSLDIR/etc/fslconf/fsl.csh
setenv FSLOUTPUTTYPE ANALYZE

# Source the FreeSurfer Environment File
set FS_ENV_FILE =  $FREESURFER_HOME/SetUpFreeSurfer.csh
if(! -e $FS_ENV_FILE) then
  echo "ERROR: cannot find $FS_ENV_FILE"
  exit 1;
endif

source $FS_ENV_FILE

set this_shell    = `basename $SHELL`;
set this_hostname = `hostname` 
set this_host     = `basename $this_hostname .nmr.mgh.harvard.edu`

### Create a prompt to indicate the standard environment ###
if($this_shell == csh) then
  unalias cd
  alias cd  'set old=$cwd; chdir \!*; set tmpcwd = `basename $cwd`; set prompt = "$this_host"":$tmpcwd (nmr-dev-env) % "'
  cd $cwd;
else
  set prompt = "[%m:%c] (nmr-dev-env) "
endif

### Dont leave huge cores around ###
limit coredumpsize 0

set path = ($path $FREESURFER_HOME/bin /usr/bin/X11 /usr/sbin /sbin)


if(! $?MRI_UMASK) then
  setenv MRI_UMASK 2
endif

rehash;

unset echo ;
unset verbose;

exit 0;
####################################################################
