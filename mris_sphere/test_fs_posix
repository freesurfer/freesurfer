#!/bin/tcsh -f

# test_fs_posix
# Paul Wighton 2017/05/16
#
# Script to test the behaviour of mris_sphere with and without the env var 
# FS_POSIX set.  When FS_POSIX is set, and output filenames do not specify a 
# path, the filename should resolve to the the current working directory 
# (as per POSIX 4:11).  When FS_POSIX is not set and output filenames do not 
# specify a path the filename should resolve to the directory of the input
# surface file (legacy behviour)
#

#
# extract test data
#
gunzip -c testdata.tar.gz | tar xvf -

cd testdata
mkdir -p outdir
cd outdir

set log = (../../test_fs_posix.log)
if (-e $log) rm -f $log

pwd
ls -l ../../mris_sphere

# Test legacy behaviour
set cmd=(../../mris_sphere -q -seed 1234 ../rh.inflated rh.posix.sphere)

echo "\n\n $cmd \n\n" |& tee -a $log
$cmd |& tee -a $log

if ( $status != 0 ) then
  echo "mris_sphere/test_fs_posix -- Legacy bahviour test: FAILED (status code is non-zero)" |& tee -a $log
  exit 1
endif
if ( ! -f "../rh.posix.sphere" ) then
  echo "mris_sphere/test_fs_posix -- Legacy bahviour test: FAILED (output file in wrong place)" |& tee -a $log
  exit 1
endif

echo "" |& tee -a $log
echo "" |& tee -a $log
echo "" |& tee -a $log

# Test POSIX behaviour
setenv FS_POSIX 1
set cmd=(../../mris_sphere -q -seed 1234 ../rh.inflated rh.posix.sphere)
echo "\n\n $cmd \n\n" |& tee -a $log
$cmd |& tee -a $log
if ( $status != 0 ) then
  echo "mris_sphere/test_fs_posix -- POSIX bahviour test: FAILED (status code is non-zero)" |& tee -a $log
  exit 1
endif
if ( ! -f "./rh.posix.sphere" ) then
  echo "mris_sphere/test_fs_posix -- POSIX bahviour test: FAILED (output file in wrong place)" |& tee -a $log
  exit 1
endif

#
# cleanup
#
cd ../..
rm -Rf testdata

echo "" |& tee -a $log
echo "test_mris_sphere passed all tests" |& tee -a $log
exit 0
