#!/bin/tcsh -f

#
# test_mris_sphere
#
# execute mris_sphere and check results against expected results
#
# Original Author: Nick Schmansky
# CVS Revision Info:
#    $Author: nicks $
#    $Date: 2014/11/04 18:52:06 $
#    $Revision: 1.3 $
#
# Copyright Â© 2014 The General Hospital Corporation (Boston, MA) "MGH"
#
# Terms and conditions for use, reproduction, distribution and contribution
# are found in the 'FreeSurfer Software License Agreement' contained
# in the file 'LICENSE' found in the FreeSurfer distribution, and here:
#
# https://surfer.nmr.mgh.harvard.edu/fswiki/FreeSurferSoftwareLicense
#
# Reporting: freesurfer@nmr.mgh.harvard.edu
#
# General inquiries: freesurfer@nmr.mgh.harvard.edu
#

set VERSION='$Id: test_mris_sphere,v 1.3 2014/11/04 18:52:06 nicks Exp $'

umask 002

#
# extract testing data
#
gunzip -c testdata.tar.gz | tar xvf -

#
# run mris_sphere using typical input files,
# first with one thread then with 8 threads.
#

cd testdata

set log = (../test_mris_sphere.log)
if (-e $log) rm -f $log

setenv SKIP_MRIS_SPHERE_1CPU_TEST
set threads = ( 1 8 )
if ($?SKIP_MRIS_SPHERE_1CPU_TEST) set threads = ( 8 )

foreach num ($threads)

    setenv OMP_NUM_THREADS $num
    set cmd=(../mris_sphere -seed 1234 rh.inflated rh.${num}cpu.sphere)
    echo "\n\n $cmd \n\n" |& tee -a $log
    $cmd |& tee -a $log
    if ($status != 0) then
        echo "mris_sphere FAILED" |& tee -a $log
        exit 1
    endif

end

echo "" |& tee -a $log
echo "" |& tee -a $log
echo "" |& tee -a $log

#
# compare expected results with actual (produced) results
#

foreach num ($threads)

  set cmd=(../../mris_diff/mris_diff \
        --debug rh.sphere rh.${num}cpu.sphere);
  echo "\n\n $cmd \n\n" |& tee -a $log
  $cmd |& tee -a $log
  set diff_status=$status
  if ($diff_status != 0) then
    echo "$cmd FAILED (exit status=$diff_status)" |& tee -a $log
    exit 1
  endif

end

#
# cleanup
#
cd ..
rm -Rf testdata

echo "" |& tee -a $log
echo "test_mris_sphere passed all tests" |& tee -a $log
exit 0
